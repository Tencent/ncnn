#version 450

layout (constant_id = 0) const int kernel_w = 1;
layout (constant_id = 1) const int kernel_h = 1;
layout (constant_id = 2) const int dilation_w = 1;
layout (constant_id = 3) const int dilation_h = 1;
layout (constant_id = 4) const int stride_w = 1;
layout (constant_id = 5) const int stride_h = 1;
layout (constant_id = 6) const int bias_term = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

layout (binding = 0) readonly buffer bottom_blob { float bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { float top_blob_data[]; };
layout (binding = 2) readonly buffer weight { float weight_data[]; };
layout (binding = 3) readonly buffer bias { float bias_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.outw || gy >= p.outh || gz >= p.outc)
        return;

    float sum = 0;

    if (bias_term == 1)
    {
        sum = bias_data[gz];
    }

    int w_offset = gz * p.c * kernel_w * kernel_h;

    for (int z = 0; z < p.c; z++)
    {
        int v_offset = z * p.cstep + gy * stride_h * p.w + gx * stride_w;

        for (int y = 0; y < kernel_h; y++)
        {
            for (int x = 0; x < kernel_w; x++)
            {
                sum += weight_data[w_offset + x] * bottom_blob_data[v_offset + x * dilation_w];
            }

            v_offset += dilation_h * p.w;
            w_offset += kernel_w;
        }
    }

    top_blob_data[gz * p.outcstep + gy * p.outw + gx] = sum;
}
