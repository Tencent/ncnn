// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

layout (constant_id = 0) const int clip = 0;
layout (constant_id = 1) const float offset = 0;
layout (constant_id = 2) const int num_sizes = 0;
layout (constant_id = 3) const int num_ratios = 0;
layout (constant_id = 4) const int num_prior = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

layout (binding = 0) writeonly buffer top_blob { vec4 top_blob_data[]; };
layout (binding = 1) readonly buffer min_sizes { float min_sizes_data[]; };
layout (binding = 2) readonly buffer aspect_ratios { float aspect_ratios_data[]; };

layout (push_constant) uniform parameter
{
    int w;
    int h;

    float step_w;
    float step_h;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= num_sizes || gy >= p.w || gz >= p.h)
        return;

    // mxnet style _contrib_MultiBoxPrior
    int v_offset = (gz * p.w + gy) * num_prior + gx;

    float center_x = (gy + offset) * p.step_w;
    float center_y = (gz + offset) * p.step_h;
    vec4 center = vec4(center_x, center_y, center_x, center_y);

    // ratio = 1, various sizes
    float size = min_sizes_data[gx];
    float cw = size * p.h / p.w / 2;
    float ch = size / 2;

    vec4 box = center + vec4(-cw, -ch, cw, ch);

    top_blob_data[v_offset] = clip == 1 ? clamp(box, 0.f, 1.f) : box;

    if (gx == num_sizes - 1)
    {
        // various ratios, size = min_size = size[0]
        float size = min_sizes_data[0];
        for (int pi = 1; pi < num_ratios; pi++)
        {
            float ratio = sqrt(aspect_ratios_data[pi]);
            float cwr = size * p.h / p.w * ratio / 2;
            float chr = size / ratio / 2;

            vec4 box = center + vec4(-cwr, -chr, cwr, chr);

            top_blob_data[v_offset + pi] = clip == 1 ? clamp(box, 0.f, 1.f) : box;
        }
    }
}
