// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

layout (constant_id = 0) const int top = 0;
layout (constant_id = 1) const int bottom = 1;
layout (constant_id = 2) const int left = 1;
layout (constant_id = 3) const int right = 1;
layout (constant_id = 4) const int type = 1;
layout (constant_id = 5) const float value = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

layout (binding = 0) readonly buffer bottom_blob { vec4 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { vec4 top_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.outw || gy >= p.outh || gz >= p.outc)
        return;

    vec4 res;

    int x = gx - left;
    int y = gy - top;

    if (type == 0)
    {
        if (x >= 0 && x < p.w && y >= 0 && y < p.h)
        {
            int v_offset = gz * p.cstep + y * p.w + x;
            res = bottom_blob_data[v_offset];
        }
        else
        {
            res = vec4(value);
        }
    }
    else if (type == 1)
    {
        x = clamp(x, 0, p.w - 1);
        y = clamp(y, 0, p.h - 1);

        int v_offset = gz * p.cstep + y * p.w + x;
        res = bottom_blob_data[v_offset];
    }

    top_blob_data[gz * p.outcstep + gy * p.outw + gx] = res;
}
