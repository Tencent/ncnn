// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

layout (constant_id = 0) const int stride = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

layout (binding = 0) readonly buffer bottom_blob { vec4 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { vec4 top_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.outw || gy >= p.outh || gz >= p.outc)
        return;

    ivec4 gz4 = ivec4(gz * 4) + ivec4(0, 1, 2, 3);

    ivec4 z4 = gz4 / (stride * stride);
    ivec4 zi4 = gz4 % (stride * stride);
    ivec4 y4 = gy * stride + zi4 / stride;
    ivec4 x4 = gx * stride + zi4 % stride;

    ivec4 v_offset = (z4 / 4) * p.cstep + y4 * p.w + x4;

    ivec4 lane4 = z4 % 4;

    // v = v4[lane]
    vec4 v;

    vec4 v4;

    v4 = bottom_blob_data[v_offset.r];
    if (lane4.r == 0) v.r = v4.r;
    else if (lane4.r == 1) v.r = v4.g;
    else if (lane4.r == 2) v.r = v4.b;
    else /* if (lane4.r == 3) */ v.r = v4.a;

    v4 = bottom_blob_data[v_offset.g];
    if (lane4.g == 0) v.g = v4.r;
    else if (lane4.g == 1) v.g = v4.g;
    else if (lane4.g == 2) v.g = v4.b;
    else /* if (lane4.g == 3) */ v.g = v4.a;

    v4 = bottom_blob_data[v_offset.b];
    if (lane4.b == 0) v.b = v4.r;
    else if (lane4.b == 1) v.b = v4.g;
    else if (lane4.b == 2) v.b = v4.b;
    else /* if (lane4.b == 3) */ v.b = v4.a;

    v4 = bottom_blob_data[v_offset.a];
    if (lane4.a == 0) v.a = v4.r;
    else if (lane4.a == 1) v.a = v4.g;
    else if (lane4.a == 2) v.a = v4.b;
    else /* if (lane4.a == 3) */ v.a = v4.a;

    top_blob_data[gz * p.outcstep + gy * p.outw + gx] = v;
}
