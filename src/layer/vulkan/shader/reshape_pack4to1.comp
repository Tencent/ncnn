// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int ndim = 0;

#define shape_constant_id_offset 1
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler3D bottom_blob_3d;
layout (binding = 1, imfmtc1) writeonly uniform unfp image3D top_blob_3d;
#else
layout (binding = 0) readonly buffer bottom_blob { sfpvec4 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfp top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(w) || gy >= psc(h) || gz >= psc(c))
        return;

    ivec3 gxyz = ivec3(gx, gy, gz);

    gxyz[psc(dims) - 1] *= 4;

    int i4_0 = gxyz.z * psc(h) * psc(w) + gxyz.y * psc(w) + gxyz.x;

    ivec3 gxyz4 = ivec3(1, psc(w), psc(h) * psc(w));

    ivec4 i4 = i4_0 + ivec4(0, 1, 2, 3) * gxyz4[psc(dims) - 1];

#if NCNN_image_shader
    afpvec4 v;

    if (psc(dims) == 1)
    {
        v = image3d_ld4(bottom_blob_3d, ivec3(gx, 0, 0));
    }
    else if (psc(dims) == 2)
    {
        v = image3d_ld4(bottom_blob_3d, ivec3(gx, gy, 0));
    }
    else // if (psc(dims) == 3)
    {
        v = image3d_ld4(bottom_blob_3d, ivec3(gx, gy, gz));
    }

    if (ndim == 1)
    {
        ivec4 x4 = i4;

        image3d_st1(top_blob_3d, ivec3(x4.r, 0, 0), v.r);
        image3d_st1(top_blob_3d, ivec3(x4.g, 0, 0), v.g);
        image3d_st1(top_blob_3d, ivec3(x4.b, 0, 0), v.b);
        image3d_st1(top_blob_3d, ivec3(x4.a, 0, 0), v.a);
    }
    if (ndim == 2)
    {
        ivec4 y4 = i4 / psc(outw);
        ivec4 x4 = i4 % psc(outw);

        image3d_st1(top_blob_3d, ivec3(x4.r, y4.r, 0), v.r);
        image3d_st1(top_blob_3d, ivec3(x4.g, y4.g, 0), v.g);
        image3d_st1(top_blob_3d, ivec3(x4.b, y4.b, 0), v.b);
        image3d_st1(top_blob_3d, ivec3(x4.a, y4.a, 0), v.a);
    }
    if (ndim == 3)
    {
        int size = psc(outw) * psc(outh);

        ivec4 z4 = i4 / size;
        ivec4 y4 = i4 % size / psc(outw);
        ivec4 x4 = i4 % size % psc(outw);

        image3d_st1(top_blob_3d, ivec3(x4.r, y4.r, z4.r), v.r);
        image3d_st1(top_blob_3d, ivec3(x4.g, y4.g, z4.g), v.g);
        image3d_st1(top_blob_3d, ivec3(x4.b, y4.b, z4.b), v.b);
        image3d_st1(top_blob_3d, ivec3(x4.a, y4.a, z4.a), v.a);
    }
#else
    ivec4 v_offset;

    if (ndim == 1)
    {
        v_offset = i4;
    }
    if (ndim == 2)
    {
        ivec4 y4 = i4 / psc(outw);
        ivec4 x4 = i4 % psc(outw);

        v_offset = y4 * psc(outw) + x4;
    }
    if (ndim == 3)
    {
        int size = psc(outw) * psc(outh);

        ivec4 z4 = i4 / size;
        ivec4 y4 = i4 % size / psc(outw);
        ivec4 x4 = i4 % size % psc(outw);

        v_offset = z4 * psc(outcstep) + y4 * psc(outw) + x4;
    }

    int gi = gz * psc(cstep) + gy * psc(w) + gx;

    buffer_cp4to1(top_blob_data, v_offset, bottom_blob_data, gi);
#endif
}
