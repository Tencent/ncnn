// Copyright 2026 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout(constant_id = 0) const int attn_mask = 0;
layout(constant_id = 1) const float scale = 0.f;
layout(constant_id = 2) const int M = 0;
layout(constant_id = 3) const int N = 0;
layout(constant_id = 4) const int K = 0;
layout(constant_id = 5) const int B = 0;
layout(constant_id = 6) const int attn_mask_dims = 0;
layout(constant_id = 7) const int num_heads_per_group = 0;
layout(constant_id = 8) const int q_cstep = 0;
layout(constant_id = 9) const int k_cstep = 0;
layout(constant_id = 10) const int qk_cstep = 0;
layout(constant_id = 11) const int mask_cstep = 0;

layout(binding = 0) readonly buffer q_blob { sfp q_blob_data[]; };
layout(binding = 1) readonly buffer k_blob { sfp k_blob_data[]; };
layout(binding = 2) writeonly buffer qkcross_blob { sfp qkcross_blob_data[]; };
layout(binding = 3) readonly buffer attn_mask_blob { sfp attn_mask_blob_data[]; };

layout(push_constant) uniform parameter
{
    float scale;
    int M;
    int N;
    int K;
    int B;
    int attn_mask_dims;
    int num_heads_per_group;
    int q_cstep;
    int k_cstep;
    int qk_cstep;
    int mask_cstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(N) || gy >= psc(M) || gz >= psc(B))
        return;

    afp sum = afp(0.f);

    for (int k = 0; k < psc(K); k++)
    {
        const int ai = gz * psc(q_cstep) + gy * psc(K) + k;
        afp q0 = buffer_ld1(q_blob_data, ai);

        const int bi = (gz / psc(num_heads_per_group)) * psc(k_cstep) + gx * psc(K) + k;
        afp k0 = buffer_ld1(k_blob_data, bi);

        sum += q0 * k0;
    }

    sum *= afp(psc(scale));

    if (attn_mask == 1)
    {
        if (psc(attn_mask_dims) == 3)
        {
            const int mi = gz * psc(mask_cstep) + gy * psc(N) + gx;
            sum += buffer_ld1(attn_mask_blob_data, mi);
        }
        else
        {
            const int mi = gy * psc(N) + gx;
            sum += buffer_ld1(attn_mask_blob_data, mi);
        }
    }

    const int gi = gz * psc(qk_cstep) + gy * psc(N) + gx;
    buffer_st1(qkcross_blob_data, gi, sum);
}
