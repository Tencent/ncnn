// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2020 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
struct sfpvec8 { f16vec4 abcd; f16vec4 efgh; };
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int axis = 0;

#define shape_constant_id_offset 1
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

layout (binding = 0) readonly buffer bottom_top_blob { sfpvec8 bottom_top_blob_data[]; };
layout (binding = 1) writeonly buffer max_workspace { sfpvec8 max_workspace_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    afpvec8 max_value = afpvec8(afpvec4(-99999999.f), afpvec4(-99999999.f));

    if (psc(dims) == 1) // axis == 0
    {
        for (int i = 0; i < psc(w); i++)
        {
            afpvec8 v = buffer_ld8(bottom_top_blob_data, i);
            max_value[0] = max(max_value[0], v[0]);
            max_value[1] = max(max_value[1], v[1]);
        }
        afpvec4 max4 = max(max_value[0], max_value[1]);
        afpvec2 max2 = max(max4.rg, max4.ba);
        afp max1 = max(max2.r, max2.g);
        max_value = afpvec8(afpvec4(max1), afpvec4(max1));
        buffer_st8(max_workspace_data, 0, max_value);
        return;
    }

    if (psc(dims) == 2 && axis == 0)
    {
        for (int i = 0; i < psc(h); i++)
        {
            int v_offset = i * psc(w) + gx;
            afpvec8 v = buffer_ld8(bottom_top_blob_data, v_offset);
            max_value[0] = max(max_value[0], v[0]);
            max_value[1] = max(max_value[1], v[1]);
        }
        afpvec4 max4 = max(max_value[0], max_value[1]);
        afpvec2 max2 = max(max4.rg, max4.ba);
        afp max1 = max(max2.r, max2.g);
        max_value = afpvec8(afpvec4(max1), afpvec4(max1));
        buffer_st8(max_workspace_data, gx, max_value);
        return;
    }

    if (psc(dims) == 2 && axis == 1)
    {
        for (int i = 0; i < psc(w); i++)
        {
            int v_offset = gx * psc(w) + i;
            afpvec8 v = buffer_ld8(bottom_top_blob_data, v_offset);
            max_value[0] = max(max_value[0], v[0]);
            max_value[1] = max(max_value[1], v[1]);
        }
        buffer_st8(max_workspace_data, gx, max_value);
        return;
    }

    if (psc(dims) == 3 && axis == 0)
    {
        for (int i = 0; i < psc(c); i++)
        {
            int v_offset = i * psc(cstep) + gy * psc(w) + gx;
            afpvec8 v = buffer_ld8(bottom_top_blob_data, v_offset);
            max_value[0] = max(max_value[0], v[0]);
            max_value[1] = max(max_value[1], v[1]);
        }
        afpvec4 max4 = max(max_value[0], max_value[1]);
        afpvec2 max2 = max(max4.rg, max4.ba);
        afp max1 = max(max2.r, max2.g);
        max_value = afpvec8(afpvec4(max1), afpvec4(max1));
        buffer_st8(max_workspace_data, gy * psc(w) + gx, max_value);
        return;
    }

    if (psc(dims) == 3 && axis == 1)
    {
        for (int i = 0; i < psc(h); i++)
        {
            int v_offset = gy * psc(cstep) + i * psc(w) + gx;
            afpvec8 v = buffer_ld8(bottom_top_blob_data, v_offset);
            max_value[0] = max(max_value[0], v[0]);
            max_value[1] = max(max_value[1], v[1]);
        }
        buffer_st8(max_workspace_data, gy * psc(w) + gx, max_value);
        return;
    }

    if (psc(dims) == 3 && axis == 2)
    {
        for (int i = 0; i < psc(w); i++)
        {
            int v_offset = gy * psc(cstep) + gx * psc(w) + i;
            afpvec8 v = buffer_ld8(bottom_top_blob_data, v_offset);
            max_value[0] = max(max_value[0], v[0]);
            max_value[1] = max(max_value[1], v[1]);
        }
        buffer_st8(max_workspace_data, gy * psc(h) + gx, max_value);
        return;
    }
}
