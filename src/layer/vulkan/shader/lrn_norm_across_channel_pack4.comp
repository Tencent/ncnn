// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int region_type = 0;
layout (constant_id = 1) const int local_size = 0;
layout (constant_id = 2) const float alpha = 0;
layout (constant_id = 3) const float beta = 0;
layout (constant_id = 4) const float bias_constant = 0;

#define shape_constant_id_offset 5
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

#if NCNN_image_shader
layout (binding = 0) uniform highp sampler3D square_workspace;
layout (binding = 1) uniform unfp sampler3D bottom_blob;
layout (binding = 2, imfmtc4) writeonly uniform unfp image3D top_blob;
#else
layout (binding = 0) readonly buffer square_workspace { float square_workspace_data[]; };
layout (binding = 1) buffer bottom_top_blob { sfpvec4 bottom_top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    // support region_type == 0 only

    afpvec4 sum = afpvec4(0.f);

#if NCNN_image_shader
    ivec4 z4 = gz * 4 + ivec4(0, 1, 2, 3);

    for (int z = 0; z < local_size; z++)
    {
        sum.r += afp(texelFetch(square_workspace, ivec3(gx, gy, z4.r), 0).r);
        sum.g += afp(texelFetch(square_workspace, ivec3(gx, gy, z4.g), 0).r);
        sum.b += afp(texelFetch(square_workspace, ivec3(gx, gy, z4.b), 0).r);
        sum.a += afp(texelFetch(square_workspace, ivec3(gx, gy, z4.a), 0).r);

        z4 += 1;
    }
#else
    ivec4 z4 = gz * 4 + ivec4(0, 1, 2, 3);
    ivec4 v_offset = z4 * psc(cstep) + gy * psc(w) + gx;

    for (int z = 0; z < local_size; z++)
    {
        sum.r += afp(square_workspace_data[v_offset.r]);
        sum.g += afp(square_workspace_data[v_offset.g]);
        sum.b += afp(square_workspace_data[v_offset.b]);
        sum.a += afp(square_workspace_data[v_offset.a]);

        v_offset += psc(cstep);
    }
#endif

    const afp alpha_div_size = afp(alpha / local_size);
    afpvec4 scale = pow(afp(bias_constant) + alpha_div_size * sum, afpvec4(-beta));

#if NCNN_image_shader
    afpvec4 v = image3d_ld4(bottom_blob, ivec3(gx, gy, gz));
#else
    int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

    afpvec4 v = buffer_ld4(bottom_top_blob_data, gi);
#endif

    v *= scale;

#if NCNN_image_shader
    image3d_st4(top_blob, ivec3(gx, gy, gz), v);
#else
    buffer_st4(bottom_top_blob_data, gi, v);
#endif
}
