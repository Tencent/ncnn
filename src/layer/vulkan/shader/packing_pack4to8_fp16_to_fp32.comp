// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2020 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
struct sfpvec8 { f16vec4 abcd; f16vec4 efgh; };
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int storage_type_from = 0;
layout (constant_id = 1) const int storage_type_to = 0;

#define shape_constant_id_offset 2
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

layout (binding = 0) readonly buffer bottom_blob { sfpvec4 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob_fp32 { mat2x4 top_blob_fp32_data[]; };
#if NCNN_image_shader
layout (binding = 2) uniform unfp sampler1D bottom_blob_1d;
layout (binding = 2) uniform unfp sampler2D bottom_blob_2d;
layout (binding = 2) uniform unfp sampler3D bottom_blob_3d;
layout (binding = 3, rgba32f) writeonly uniform highp image1D top_blob_1d_fp32;
layout (binding = 3, rgba32f) writeonly uniform highp image2D top_blob_2d_fp32;
layout (binding = 3, rgba32f) writeonly uniform highp image3D top_blob_3d_fp32;
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    afpvec8 v;

    if (psc(dims) == 1)
    {
        if (storage_type_from == 0)
        {
            ivec2 x2 = ivec2(gx * 2) + ivec2(0, 1);

            ivec2 v_offset = x2;

            v[0] = buffer_ld4(bottom_blob_data, v_offset.r);
            v[1] = buffer_ld4(bottom_blob_data, v_offset.g);
        }
#if NCNN_image_shader
        if (storage_type_from == 1)
        {
            int x2 = gx * 2;

            v[0] = image1d_ld4(bottom_blob_1d, x2 + 0);
            v[1] = image1d_ld4(bottom_blob_1d, x2 + 1);
        }
#endif

        if (storage_type_to == 0)
        {
            int gi = gx;

            top_blob_fp32_data[gi] = mat2x4(vec4(v[0]), vec4(v[1]));
        }
#if NCNN_image_shader
        if (storage_type_to == 1)
        {
            image1d_st8(top_blob_1d_fp32, gx, v);
        }
#endif
    }
    else if (psc(dims) == 2)
    {
        if (storage_type_from == 0)
        {
            ivec2 y2 = ivec2(gy * 2) + ivec2(0, 1);

            ivec2 v_offset = y2 * psc(w) + gx;

            v[0] = buffer_ld4(bottom_blob_data, v_offset.r);
            v[1] = buffer_ld4(bottom_blob_data, v_offset.g);
        }
#if NCNN_image_shader
        if (storage_type_from == 1)
        {
            int y2 = gy * 2;

            v[0] = image2d_ld4(bottom_blob_2d, ivec2(gx, y2 + 0));
            v[1] = image2d_ld4(bottom_blob_2d, ivec2(gx, y2 + 1));
        }
#endif

        if (storage_type_to == 0)
        {
            int gi = gy * psc(outw) + gx;

            top_blob_fp32_data[gi] = mat2x4(vec4(v[0]), vec4(v[1]));
        }
#if NCNN_image_shader
        if (storage_type_to == 1)
        {
            image2d_st8(top_blob_2d_fp32, ivec2(gx, gy), v);
        }
#endif
    }
    else // if (psc(dims) == 3)
    {
        if (storage_type_from == 0)
        {
            ivec2 z2 = ivec2(gz * 2) + ivec2(0, 1);

            ivec2 v_offset = z2 * psc(cstep) + ivec2(gy * psc(w) + gx);

            v[0] = buffer_ld4(bottom_blob_data, v_offset.r);
            v[1] = buffer_ld4(bottom_blob_data, v_offset.g);
        }
#if NCNN_image_shader
        if (storage_type_from == 1)
        {
            int z2 = gz * 2;

            v[0] = image3d_ld4(bottom_blob_3d, ivec3(gx, gy, z2 + 0));
            v[1] = image3d_ld4(bottom_blob_3d, ivec3(gx, gy, z2 + 1));
        }
#endif

        if (storage_type_to == 0)
        {
            int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

            top_blob_fp32_data[gi] = mat2x4(vec4(v[0]), vec4(v[1]));
        }
#if NCNN_image_shader
        if (storage_type_to == 1)
        {
            image3d_st8(top_blob_3d_fp32, ivec3(gx, gy, gz), v);
        }
#endif
    }
}
