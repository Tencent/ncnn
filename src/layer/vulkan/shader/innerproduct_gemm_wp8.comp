// Copyright 2021 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

#extension GL_GOOGLE_include_directive: enable
#include "vulkan_activation.comp"

layout (constant_id = 0) const int bias_term = 0;
layout (constant_id = 1) const int activation_type = 0;
layout (constant_id = 2) const float activation_param_0 = 0;
layout (constant_id = 3) const float activation_param_1 = 0;

#define shape_constant_id_offset 4
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfp top_blob_data[]; };
layout (binding = 2) readonly buffer weight_blob { sfpvec8 weight_data[]; };
layout (binding = 3) readonly buffer bias_blob { sfpvec8 bias_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx * 8 >= psc(outw) || gy >= psc(outh) || gz >= 1)
        return;

    afpvec8 sum;

    if (bias_term == 1)
    {
        sum = buffer_ld8(bias_data, gx);
    }
    else
    {
        sum = afpvec8(afpvec4(0.f), afpvec4(0.f));
    }

    int v_offset = gy * psc(w);
    int w_offset = gx * psc(w) / 8;

    for (int i = 0; i < psc(w) / 8; i++)
    {
        afpvec8 v;
        v[0].r = buffer_ld1(bottom_blob_data, v_offset + i * 8 + 0);
        v[0].g = buffer_ld1(bottom_blob_data, v_offset + i * 8 + 1);
        v[0].b = buffer_ld1(bottom_blob_data, v_offset + i * 8 + 2);
        v[0].a = buffer_ld1(bottom_blob_data, v_offset + i * 8 + 3);
        v[1].r = buffer_ld1(bottom_blob_data, v_offset + i * 8 + 4);
        v[1].g = buffer_ld1(bottom_blob_data, v_offset + i * 8 + 5);
        v[1].b = buffer_ld1(bottom_blob_data, v_offset + i * 8 + 6);
        v[1].a = buffer_ld1(bottom_blob_data, v_offset + i * 8 + 7);

        afpvec8 k0 = buffer_ld8(weight_data, (w_offset + i) * 8 + 0);
        afpvec8 k1 = buffer_ld8(weight_data, (w_offset + i) * 8 + 1);
        afpvec8 k2 = buffer_ld8(weight_data, (w_offset + i) * 8 + 2);
        afpvec8 k3 = buffer_ld8(weight_data, (w_offset + i) * 8 + 3);
        afpvec8 k4 = buffer_ld8(weight_data, (w_offset + i) * 8 + 4);
        afpvec8 k5 = buffer_ld8(weight_data, (w_offset + i) * 8 + 5);
        afpvec8 k6 = buffer_ld8(weight_data, (w_offset + i) * 8 + 6);
        afpvec8 k7 = buffer_ld8(weight_data, (w_offset + i) * 8 + 7);

        // sum += v * k
        sum[0].r += dot(v[0], k0[0]) + dot(v[1], k0[1]);
        sum[0].g += dot(v[0], k1[0]) + dot(v[1], k1[1]);
        sum[0].b += dot(v[0], k2[0]) + dot(v[1], k2[1]);
        sum[0].a += dot(v[0], k3[0]) + dot(v[1], k3[1]);
        sum[1].r += dot(v[0], k4[0]) + dot(v[1], k4[1]);
        sum[1].g += dot(v[0], k5[0]) + dot(v[1], k5[1]);
        sum[1].b += dot(v[0], k6[0]) + dot(v[1], k6[1]);
        sum[1].a += dot(v[0], k7[0]) + dot(v[1], k7[1]);
    }

    sum = activation_afpvec8(sum, activation_type, activation_param_0, activation_param_1);

    const int gi = gy * psc(outw) + gx * 8;
    buffer_st1(top_blob_data, gi + 0, sum[0].r);
    buffer_st1(top_blob_data, gi + 1, sum[0].g);
    buffer_st1(top_blob_data, gi + 2, sum[0].b);
    buffer_st1(top_blob_data, gi + 3, sum[0].a);
    buffer_st1(top_blob_data, gi + 4, sum[1].r);
    buffer_st1(top_blob_data, gi + 5, sum[1].g);
    buffer_st1(top_blob_data, gi + 6, sum[1].b);
    buffer_st1(top_blob_data, gi + 7, sum[1].a);
}
