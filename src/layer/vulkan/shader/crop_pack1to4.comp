// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int bugihfa = 0;

#define shape_constant_id_offset 1
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler3D bottom_blob;
layout (binding = 1, imfmtc4) writeonly uniform unfp image3D top_blob;
#else
layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfpvec4 top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;

    int woffset;
    int hoffset;
    int coffset;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    if (psc(dims) == 1)
    {
        int x = gx * 4 + p.woffset;

#if NCNN_image_shader
        afpvec4 v;
        v.r = image3d_ld1(bottom_blob, ivec3(x + 0, 0, 0));
        v.g = image3d_ld1(bottom_blob, ivec3(x + 1, 0, 0));
        v.b = image3d_ld1(bottom_blob, ivec3(x + 2, 0, 0));
        v.a = image3d_ld1(bottom_blob, ivec3(x + 3, 0, 0));

        image3d_st4(top_blob, ivec3(gx, 0, 0), v);
#else
        ivec4 v_offset = x + ivec4(0, 1, 2, 3);

        buffer_cp1to4(top_blob_data, gx, bottom_blob_data, v_offset);
#endif
    }
    else if (psc(dims) == 2)
    {
        int x = gx + p.woffset;
        int y = gy * 4 + p.hoffset;

#if NCNN_image_shader
        afpvec4 v;
        v.r = image3d_ld1(bottom_blob, ivec3(x, y + 0, 0));
        v.g = image3d_ld1(bottom_blob, ivec3(x, y + 1, 0));
        v.b = image3d_ld1(bottom_blob, ivec3(x, y + 2, 0));
        v.a = image3d_ld1(bottom_blob, ivec3(x, y + 3, 0));

        image3d_st4(top_blob, ivec3(gx, gy, 0), v);
#else
        int gi = gy * psc(outw) + gx;

        ivec4 v_offset = y * psc(w) + x + ivec4(0, 1, 2, 3) * psc(w);

        buffer_cp1to4(top_blob_data, gi, bottom_blob_data, v_offset);
#endif
    }
    else // if (psc(dims) == 3)
    {
        int x = gx + p.woffset;
        int y = gy + p.hoffset;
        int z = gz * 4 + p.coffset;

#if NCNN_image_shader
        afpvec4 v;
        v.r = image3d_ld1(bottom_blob, ivec3(x, y, z + 0));
        v.g = image3d_ld1(bottom_blob, ivec3(x, y, z + 1));
        v.b = image3d_ld1(bottom_blob, ivec3(x, y, z + 2));
        v.a = image3d_ld1(bottom_blob, ivec3(x, y, z + 3));

        image3d_st4(top_blob, ivec3(gx, gy, gz), v);
#else
        int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

        ivec4 v_offset = z * psc(cstep) + y * psc(w) + x + ivec4(0, 1, 2, 3) * psc(cstep);

        buffer_cp1to4(top_blob_data, gi, bottom_blob_data, v_offset);
#endif
    }
}
