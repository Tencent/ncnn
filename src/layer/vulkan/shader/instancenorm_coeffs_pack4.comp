// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const float eps = 0;
layout (constant_id = 1) const int affine = 0;
layout (constant_id = 2) const int w = 0;

#if NCNN_image_shader
layout (binding = 0, imfmtc4) writeonly uniform unfp image3D coeffs_blob;
layout (binding = 1) uniform highp sampler3D mean_blob;
layout (binding = 2) uniform highp sampler3D var_blob;
layout (binding = 3) uniform unfp sampler3D gamma_blob;
layout (binding = 4) uniform unfp sampler3D beta_blob;
#else
layout (binding = 0) writeonly buffer coeffs_blob { sfpvec4 coeffs_blob_data[]; };
layout (binding = 1) readonly buffer mean_blob { vec4 mean_data[]; };
layout (binding = 2) readonly buffer var_blob { vec4 var_data[]; };
layout (binding = 3) readonly buffer gamma_blob { sfpvec4 gamma_data[]; };
layout (binding = 4) readonly buffer beta_blob { sfpvec4 beta_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int w;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(w) || gy >= 1 || gz >= 1)
        return;

#if NCNN_image_shader
    vec4 mean = texelFetch(mean_blob, ivec3(gx, 0, 0), 0);
    vec4 var = texelFetch(var_blob, ivec3(gx, 0, 0), 0);
#else
    vec4 mean = mean_data[gx];
    vec4 var = var_data[gx];
#endif

    vec4 a;
    vec4 b;
    if (affine == 0)
    {
        a = 1.f / (sqrt(var + eps));
        b = - mean * a;
    }
    else
    {
#if NCNN_image_shader
        vec4 gamma = vec4(image3d_ld4(gamma_blob, ivec3(gx, 0, 0)));
        vec4 beta = vec4(image3d_ld4(beta_blob, ivec3(gx, 0, 0)));
#else
        vec4 gamma = vec4(buffer_ld4(gamma_data, gx));
        vec4 beta = vec4(buffer_ld4(beta_data, gx));
#endif

        a = gamma / (sqrt(var + eps));
        b = - mean * a + beta;
    }

#if NCNN_image_shader
    imageStore(coeffs_blob, ivec3(gx*2, 0, 0), a);
    imageStore(coeffs_blob, ivec3(gx*2 +1, 0, 0), b);
#else
    buffer_st4(coeffs_blob_data, gx*2, afpvec4(a));
    buffer_st4(coeffs_blob_data, gx*2 +1, afpvec4(b));
#endif
}
