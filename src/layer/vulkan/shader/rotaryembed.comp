// Copyright 2026 Futz12 <pchar.cn>
// Copyright 2026 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout(constant_id = 0) const int interleaved = 0;

#define shape_constant_id_offset 1
layout(constant_id = shape_constant_id_offset + 0) const int embed_dim = 0;
layout(constant_id = shape_constant_id_offset + 1) const int seqlen = 0;
layout(constant_id = shape_constant_id_offset + 2) const int num_heads = 0;
layout(constant_id = shape_constant_id_offset + 3) const int cstep = 0;

layout(binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout(binding = 1) readonly buffer cos_cache { sfp cos_cache_data[]; };
layout(binding = 2) readonly buffer sin_cache { sfp sin_cache_data[]; };
layout(binding = 3) writeonly buffer top_blob { sfp top_blob_data[]; };

layout(push_constant) uniform parameter
{
    int embed_dim;
    int seqlen;
    int num_heads;
    int cstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    const int halfdim = psc(embed_dim) / 2;

    if (gx >= halfdim || gy >= psc(seqlen) || gz >= psc(num_heads))
        return;

    const int cache_offset = gy * halfdim + gx;

    afp cosv = buffer_ld1(cos_cache_data, cache_offset);
    afp sinv = buffer_ld1(sin_cache_data, cache_offset);

    if (interleaved == 1)
    {
        const int gi = gz * psc(cstep) + gy * psc(embed_dim) + gx * 2;

        afp x0 = buffer_ld1(bottom_blob_data, gi);
        afp x1 = buffer_ld1(bottom_blob_data, gi + 1);

        afp y0 = x0 * cosv - x1 * sinv;
        afp y1 = x0 * sinv + x1 * cosv;

        buffer_st1(top_blob_data, gi, y0);
        buffer_st1(top_blob_data, gi + 1, y1);
    }
    else
    {
        const int gi = gz * psc(cstep) + gy * psc(embed_dim) + gx;

        afp x0 = buffer_ld1(bottom_blob_data, gi);
        afp x1 = buffer_ld1(bottom_blob_data, gi + halfdim);

        afp y0 = x0 * cosv - x1 * sinv;
        afp y1 = x0 * sinv + x1 * cosv;

        buffer_st1(top_blob_data, gi, y0);
        buffer_st1(top_blob_data, gi + halfdim, y1);
    }
}
