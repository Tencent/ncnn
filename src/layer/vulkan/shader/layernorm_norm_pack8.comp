// Copyright 2025 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout (constant_id = 0) const int affine = 0;

layout (binding = 0) buffer bottom_top_blob { sfpvec8 bottom_top_blob_data[]; };
layout (binding = 1) readonly buffer coeffs_blob { sfpvec8 coeffs_blob_data[]; };
layout (binding = 2) readonly buffer gamma_blob { sfp gamma_data[]; };
layout (binding = 3) readonly buffer beta_blob { sfp beta_data[]; };

layout (push_constant) uniform parameter
{
    int w;
    int h;
    int c;
    int cstep;
    int affine_size;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.w || gy >= p.h || gz >= p.c)
        return;

    int group_id;
    int inner_id;

    if (p.affine_size == p.w)
    {
        group_id = gz * p.h + gy;
        inner_id = gx;
    }
    else // if (p.affine_size == p.w * p.h)
    {
        group_id = gz;
        inner_id = gy * p.w + gx;
    }

    afpvec8 a = buffer_ld8(coeffs_blob_data, group_id * 2);
    afpvec8 b = buffer_ld8(coeffs_blob_data, group_id * 2 + 1);

    int linear_index = gz * p.cstep + gy * p.w + gx;
    afpvec8 v = buffer_ld8(bottom_top_blob_data, linear_index);

    v[0] = v[0] * a[0] + b[0];
    v[1] = v[1] * a[1] + b[1];

    if (affine == 1)
    {
        afpvec4 gamma = afpvec4(buffer_ld1(gamma_data, inner_id));
        afpvec4 beta  = afpvec4(buffer_ld1(beta_data, inner_id));
        v[0] = v[0] * gamma + beta;
        v[1] = v[1] * gamma + beta;
    }

    buffer_st8(bottom_top_blob_data, linear_index, v);
}
