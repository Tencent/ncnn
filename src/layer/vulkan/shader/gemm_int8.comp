// Copyright 2025 pchar.cn
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout (constant_id = 0) const float alpha = 1.f;
layout (constant_id = 1) const float beta = 1.f;
layout (constant_id = 2) const int transA = 0;
layout (constant_id = 3) const int transB = 0;
layout (constant_id = 4) const int constantA = 0;
layout (constant_id = 5) const int constantB = 0;
layout (constant_id = 6) const int constantC = 0;
layout (constant_id = 7) const int M = 0;
layout (constant_id = 8) const int N = 0;
layout (constant_id = 9) const int K = 0;
layout (constant_id = 10) const int constant_broadcast_type_C = 0;
layout (constant_id = 11) const int output_N1M = 0;
layout (constant_id = 12) const int output_elempack = 0;
layout (constant_id = 13) const int output_elemtype = 0;
layout (constant_id = 14) const int output_transpose = 0;

// TODO psc more

layout (binding = 0) writeonly buffer top_blob { sfp top_blob_data[]; };
layout (binding = 1) readonly buffer A_blob { sint8 A_blob_data[]; };
layout (binding = 2) readonly buffer B_blob { sint8 B_blob_data[]; };
layout (binding = 3) readonly buffer C_blob { sfp C_blob_data[]; };
layout (binding = 4) readonly buffer A_scales { float A_scales_data[]; };
layout (binding = 5) readonly buffer B_scales { float B_scales_data[]; };

layout (push_constant) uniform parameter
{
    int M;
    int N;
    int K;
    int broadcast_type_C;
    int A_dims;
    int A_hstep;
    int B_dims;
    int B_hstep;
    int outdims;
    int outhstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x) * 2;
    int gy = int(gl_GlobalInvocationID.y) * 2;
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(N) || gy >= psc(M) || gz >= 1)
        return;

    int sum0 = 0;
    int sum1 = 0;
    int sum2 = 0;
    int sum3 = 0;

    for (int k = 0; k < psc(K); k++)
    {
        int a0;
        int a1;
        int b0;
        int b1;
        if (transA == 1)
        {
            const int ai = k * p.A_hstep + gy;
            a0 = i8buffer_ld1(A_blob_data, ai);
            a1 = i8buffer_ld1(A_blob_data, ai + 1);
        }
        else
        {
            const int ai = gy * p.A_hstep + k;
            a0 = i8buffer_ld1(A_blob_data, ai);
            a1 = i8buffer_ld1(A_blob_data, ai + p.A_hstep);
        }

        if (transB == 1)
        {
            const int bi = gx * p.B_hstep + k;
            b0 = i8buffer_ld1(B_blob_data, bi);
            b1 = i8buffer_ld1(B_blob_data, bi + p.B_hstep);
        }
        else
        {
            const int bi = k * p.B_hstep + gx;
            b0 = i8buffer_ld1(B_blob_data, bi);
            b1 = i8buffer_ld1(B_blob_data, bi + 1);
        }

        sum0 += a0 * b0;
        sum1 += a0 * b1;
        sum2 += a1 * b0;
        sum3 += a1 * b1;
    }

    afp fsum0 = afp(0.f);
    afp fsum1 = afp(0.f);
    afp fsum2 = afp(0.f);
    afp fsum3 = afp(0.f);

    const int broadcast_type_C = constantC == 1 ? constant_broadcast_type_C : p.broadcast_type_C;

    if (broadcast_type_C == 0)
    {
        fsum0 = buffer_ld1(C_blob_data, 0);
        fsum1 = fsum0;
        fsum2 = fsum0;
        fsum3 = fsum0;
    }
    if (broadcast_type_C == 1 || broadcast_type_C == 2)
    {
        fsum0 = buffer_ld1(C_blob_data, gy);
        fsum1 = fsum0;
        fsum2 = buffer_ld1(C_blob_data, gy + 1);
        fsum3 = fsum2;
    }
    if (broadcast_type_C == 3)
    {
        const int ci = gy * psc(N) + gx;
        fsum0 = buffer_ld1(C_blob_data, ci);
        fsum1 = buffer_ld1(C_blob_data, ci + 1);
        fsum2 = buffer_ld1(C_blob_data, ci + psc(N));
        fsum3 = buffer_ld1(C_blob_data, ci + psc(N) + 1);
    }
    if (broadcast_type_C == 4)
    {
        fsum0 = buffer_ld1(C_blob_data, gx);
        fsum1 = buffer_ld1(C_blob_data, gx + 1);
        fsum2 = fsum0;
        fsum3 = fsum1;
    }

    fsum0 *= afp(beta);
    fsum1 *= afp(beta);
    fsum2 *= afp(beta);
    fsum3 *= afp(beta);

    float B_int8_scale = B_scales_data[0];
    float A_int8_scale0 = A_scales_data[gy];
    float A_int8_scale1 = A_scales_data[gy + 1];

    float scale0 = A_int8_scale0 * B_int8_scale;
    float scale1 = A_int8_scale1 * B_int8_scale;

    fsum0 += float(sum0) / scale0;
    fsum1 += float(sum1) / scale0;
    fsum2 += float(sum2) / scale1;
    fsum3 += float(sum3) / scale1;

    fsum0 *= float(alpha);
    fsum1 *= float(alpha);
    fsum2 *= float(alpha);
    fsum3 *= float(alpha);

    if (output_transpose == 1)
    {
        const int gi = gx * p.outhstep + gy;

        buffer_st1(top_blob_data, gi, afp(fsum0));
        if (gy + 1 < psc(M)) buffer_st1(top_blob_data, gi + 1, afp(fsum2));
        if (gx + 1 < psc(N))
        {
            buffer_st1(top_blob_data, gi + p.outhstep, afp(fsum1));
            if (gy + 1 < psc(M)) buffer_st1(top_blob_data, gi + p.outhstep + 1, afp(fsum3));
        }
    }
    else
    {
        const int gi = gy * p.outhstep + gx;

        buffer_st1(top_blob_data, gi, afp(fsum0));
        if (gx + 1 < psc(N)) buffer_st1(top_blob_data, gi + 1, afp(fsum1));
        if (gy + 1 < psc(M))
        {
            buffer_st1(top_blob_data, gi + p.outhstep, afp(fsum2));
            if (gx + 1 < psc(N)) buffer_st1(top_blob_data, gi + p.outhstep + 1, afp(fsum3));
        }
    }
}