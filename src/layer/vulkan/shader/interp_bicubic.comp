// Copyright 2019 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

#define shape_constant_id_offset 0
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfp top_blob_data[]; };
layout (binding = 2) readonly buffer alpha_blob { sfpvec4 alpha_blob_data[]; };
layout (binding = 3) readonly buffer xofs_blob { int xofs_blob_data[]; };
layout (binding = 4) readonly buffer beta_blob { sfpvec4 beta_blob_data[]; };
layout (binding = 5) readonly buffer yofs_blob { int yofs_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    if (psc(dims) == 2)
    {
        int sx = xofs_blob_data[gx];

        int v_offset_1 = gz * psc(cstep) + gy * psc(w) + sx;

        afp b0 = buffer_ld1(bottom_blob_data, v_offset_1 - 1);
        afp b1 = buffer_ld1(bottom_blob_data, v_offset_1 + 0);
        afp b2 = buffer_ld1(bottom_blob_data, v_offset_1 + 1);
        afp b3 = buffer_ld1(bottom_blob_data, v_offset_1 + 2);

        afpvec4 alpha = buffer_ld4(alpha_blob_data, gx);

        afpvec4 abcd = afpvec4(b0, b1, b2, b3);

        afp v = dot(abcd, alpha);

        const int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

        buffer_st1(top_blob_data, gi, v);
        return;
    }

    int sx = xofs_blob_data[gx];
    int sy = yofs_blob_data[gy];

    int v_offset_0 = gz * psc(cstep) + (sy - 1) * psc(w) + sx;
    int v_offset_1 = gz * psc(cstep) + (sy + 0) * psc(w) + sx;
    int v_offset_2 = gz * psc(cstep) + (sy + 1) * psc(w) + sx;
    int v_offset_3 = gz * psc(cstep) + (sy + 2) * psc(w) + sx;

    afp a0 = buffer_ld1(bottom_blob_data, v_offset_0 - 1);
    afp a1 = buffer_ld1(bottom_blob_data, v_offset_0 + 0);
    afp a2 = buffer_ld1(bottom_blob_data, v_offset_0 + 1);
    afp a3 = buffer_ld1(bottom_blob_data, v_offset_0 + 2);

    afp b0 = buffer_ld1(bottom_blob_data, v_offset_1 - 1);
    afp b1 = buffer_ld1(bottom_blob_data, v_offset_1 + 0);
    afp b2 = buffer_ld1(bottom_blob_data, v_offset_1 + 1);
    afp b3 = buffer_ld1(bottom_blob_data, v_offset_1 + 2);

    afp c0 = buffer_ld1(bottom_blob_data, v_offset_2 - 1);
    afp c1 = buffer_ld1(bottom_blob_data, v_offset_2 + 0);
    afp c2 = buffer_ld1(bottom_blob_data, v_offset_2 + 1);
    afp c3 = buffer_ld1(bottom_blob_data, v_offset_2 + 2);

    afp d0 = buffer_ld1(bottom_blob_data, v_offset_3 - 1);
    afp d1 = buffer_ld1(bottom_blob_data, v_offset_3 + 0);
    afp d2 = buffer_ld1(bottom_blob_data, v_offset_3 + 1);
    afp d3 = buffer_ld1(bottom_blob_data, v_offset_3 + 2);

    afpmat4 abcd0123 = afpmat4(
        a0, a1, a2, a3,
        b0, b1, b2, b3,
        c0, c1, c2, c3,
        d0, d1, d2, d3
    );

    afpvec4 alpha = buffer_ld4(alpha_blob_data, gx);

    afpvec4 abcd = alpha * abcd0123;

    afpvec4 beta = buffer_ld4(beta_blob_data, gy);

    afp v = dot(abcd, beta);

    const int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

    buffer_st1(top_blob_data, gi, v);
}
