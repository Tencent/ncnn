// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2018 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

#define FLT_MAX 3.402823466e+38

layout (constant_id = 0) const int pooling_type = 0;

#define shape_constant_id_offset 1
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler3D bottom_blob;
layout (binding = 1, imfmtc1) writeonly uniform unfp image3D top_blob;
#else
layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfp top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    afp res;

    // calculate adaptive kernel size
    const int kernel_w = max(psc(w) - psc(outw) + 1, 1);
    const int kernel_h = max(psc(h) - psc(outh) + 1, 1);
    const int sx = psc(outw) == 1 ? 0 : gx * (psc(w) - kernel_w) / (psc(outw) - 1);
    const int sy = psc(outh) == 1 ? 0 : gy * (psc(h) - kernel_h) / (psc(outh) - 1);

    if (pooling_type == 0)
    {
        res = afp(-FLT_MAX);

#if NCNN_image_shader

        for (int y = 0; y < kernel_h; y++)
        {
            for (int x = 0; x < kernel_w; x++)
            {
                afp v = image3d_ld1(bottom_blob, ivec3(sx + x, sy + y, gz));
                res = max(res, v);
            }
        }
#else
        int v_offset = gz * psc(cstep) + sy * psc(w) + sx;

        for (int y = 0; y < kernel_h; y++)
        {
            for (int x = 0; x < kernel_w; x++)
            {
                afp v = buffer_ld1(bottom_blob_data, v_offset + x);
                res = max(res, v);
            }

            v_offset += psc(w);
        }
#endif
    }
    if (pooling_type == 1)
    {
        float res_fp32 = 0.f;  // force accumulation in fp32
        int area = 0;

#if NCNN_image_shader
        for (int y = 0; y < kernel_h; y++)
        {
            for (int x = 0; x < kernel_w; x++)
            {
                res_fp32 += image3d_ld1(bottom_blob, ivec3(sx + x, sy + y, gz));
                area += 1;
            }
        }
#else
        int v_offset = gz * psc(cstep) + sy * psc(w) + sx;

        for (int y = 0; y < kernel_h; y++)
        {
            for (int x = 0; x < kernel_w; x++)
            {
                res_fp32 += buffer_ld1(bottom_blob_data, v_offset + x);
                area += 1;
            }

            v_offset += psc(w);
        }
#endif

        res_fp32 /= float(area);
        res = afp(res_fp32);  // cast to fp16 if possible
    }

#if NCNN_image_shader
    image3d_st1(top_blob, ivec3(gx, gy, gz), res);
#else
    const int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

    buffer_st1(top_blob_data, gi, res);
#endif
}
