// Copyright 2026 Futz12 <pchar.cn>
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout (constant_id = 0) const int pooling_type = 0;
layout (constant_id = 1) const int out_w_param = 0;
layout (constant_id = 2) const int out_h_param = 0;
layout (constant_id = 3) const int reserved0 = 0;
layout (constant_id = 4) const int reserved1 = 0;

layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfp top_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int d;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outd;
    int outc;
    int outcstep;
} p;

void main()
{
    int ox = int(gl_GlobalInvocationID.x);
    int oy = int(gl_GlobalInvocationID.y);
    int oz = int(gl_GlobalInvocationID.z);

    if (ox >= p.outw || oy >= p.outh || oz >= p.outc)
    return;

    int ih0 = p.h * oy / p.outh;
    int ih1 = (p.h * (oy + 1) + p.outh - 1) / p.outh;

    int iw0 = p.w * ox / p.outw;
    int iw1 = (p.w * (ox + 1) + p.outw - 1) / p.outw;

    if (pooling_type == 0)
    {
        int si0 = oz * p.cstep + ih0 * p.w + iw0;
        afp mv = buffer_ld1(bottom_blob_data, si0);

        for (int iy = ih0; iy < ih1; iy++)
        {
            int base = oz * p.cstep + iy * p.w;
            for (int ix = iw0; ix < iw1; ix++)
            {
                afp v = buffer_ld1(bottom_blob_data, base + ix);
                mv = max(mv, v);
            }
        }

        int gi = oz * p.outcstep + oy * p.outw + ox;
        buffer_st1(top_blob_data, gi, mv);
    }
    else
    {
        afp sum = afp(0.f);
        int hk = ih1 - ih0;
        int wk = iw1 - iw0;
        int area = hk * wk;

        for (int iy = ih0; iy < ih1; iy++)
        {
            int base = oz * p.cstep + iy * p.w;
            for (int ix = iw0; ix < iw1; ix++)
            {
                sum += buffer_ld1(bottom_blob_data, base + ix);
            }
        }

        sum *= afp(1.f / float(area));
        int gi = oz * p.outcstep + oy * p.outw + ox;
        buffer_st1(top_blob_data, gi, sum);
    }
}
