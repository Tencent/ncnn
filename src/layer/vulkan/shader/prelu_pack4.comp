// Copyright 2019 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout (constant_id = 0) const int num_slope = 0;
layout (constant_id = 1) const float slope_value = 1.f;

#define shape_constant_id_offset 2
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (binding = 0) buffer bottom_top_blob { sfpvec4 bottom_top_blob_data[]; };
layout (binding = 1) readonly buffer slope_blob { sfpvec4 slope_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(w) || gy >= psc(h) || gz >= psc(c))
        return;

    const int gi = gz * psc(cstep) + gy * psc(w) + gx;

    afpvec4 v = buffer_ld4(bottom_top_blob_data, gi);

    afpvec4 slope;

    if (num_slope == 1)
    {
        slope = afpvec4(slope_value);
    }
    else
    {
        const ivec3 gxyz = ivec3(gx, gy, gz);
        const int abi = gxyz[psc(dims) - 1];

        slope = buffer_ld4(slope_blob_data, abi);
    }

    v = mix(v, v * slope, lessThan(v, afpvec4(0.f)));

    buffer_st4(bottom_top_blob_data, gi, v);
}
