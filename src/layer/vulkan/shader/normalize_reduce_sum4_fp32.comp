// Copyright 2019 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout (constant_id = 0) const int across_spatial = 0;
layout (constant_id = 1) const int across_channel = 0;

layout (binding = 0) readonly buffer square_blob { float square_blob_data[]; };
layout (binding = 1) writeonly buffer sqsum_blob { float sqsum_blob_data[]; };

layout (push_constant) uniform parameter
{
    int w;
    int h;
    int c;
    int cstep;

    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.outw || gy >= p.outh || gz >= p.outc)
        return;

    float sqsum;

    if (across_spatial == 1 && across_channel == 1)
    {
        int sz = gz * 2;
        int sx = gx * 2;

        ivec2 v_offset0 = sz * p.cstep + sx + ivec2(0, 1);
        ivec2 v_offset1 = v_offset0 + p.cstep;

        if (sz == p.c - 1)
        {
            if (sx == p.w - 1)
            {
                float v0 = square_blob_data[v_offset0.r];

                sqsum = v0;
            }
            else
            {
                float v0 = square_blob_data[v_offset0.r];
                float v1 = square_blob_data[v_offset0.g];

                sqsum = v0 + v1;
            }
        }
        else
        {
            if (sx == p.w - 1)
            {
                float v0 = square_blob_data[v_offset0.r];
                float v2 = square_blob_data[v_offset1.r];

                sqsum = v0 + v2;
            }
            else
            {
                float v0 = square_blob_data[v_offset0.r];
                float v1 = square_blob_data[v_offset0.g];
                float v2 = square_blob_data[v_offset1.r];
                float v3 = square_blob_data[v_offset1.g];

                sqsum = v0 + v1 + v2 + v3;
            }
        }
    }

    if (across_spatial == 1 && across_channel == 0)
    {
        int sz = gz;
        int sx = gx * 4;

        ivec4 v_offset = sz * p.cstep + sx + ivec4(0, 1, 2, 3);

        if (sx == p.w - 1)
        {
            float v0 = square_blob_data[v_offset.r];

            sqsum = v0;
        }
        else if (sx == p.w - 2)
        {
            float v0 = square_blob_data[v_offset.r];
            float v1 = square_blob_data[v_offset.g];

            sqsum = v0 + v1;
        }
        else if (sx == p.w - 3)
        {
            float v0 = square_blob_data[v_offset.r];
            float v1 = square_blob_data[v_offset.g];
            float v2 = square_blob_data[v_offset.b];

            sqsum = v0 + v1 + v2;
        }
        else
        {
            float v0 = square_blob_data[v_offset.r];
            float v1 = square_blob_data[v_offset.g];
            float v2 = square_blob_data[v_offset.b];
            float v3 = square_blob_data[v_offset.a];

            sqsum = v0 + v1 + v2 + v3;
        }
    }

    if (across_spatial == 0 && across_channel == 1)
    {
        int sz = gz * 4;
        int sx = gx;

        ivec4 v_offset = (sz + ivec4(0, 1, 2, 3)) * p.cstep + sx;

        if (sz == p.c - 1)
        {
            float v0 = square_blob_data[v_offset.r];

            sqsum = v0;
        }
        else if (sz == p.c - 2)
        {
            float v0 = square_blob_data[v_offset.r];
            float v1 = square_blob_data[v_offset.g];

            sqsum = v0 + v1;
        }
        else if (sz == p.c - 3)
        {
            float v0 = square_blob_data[v_offset.r];
            float v1 = square_blob_data[v_offset.g];
            float v2 = square_blob_data[v_offset.b];

            sqsum = v0 + v1 + v2;
        }
        else
        {
            float v0 = square_blob_data[v_offset.r];
            float v1 = square_blob_data[v_offset.g];
            float v2 = square_blob_data[v_offset.b];
            float v3 = square_blob_data[v_offset.a];

            sqsum = v0 + v1 + v2 + v3;
        }
    }

    int gi = gz * p.outcstep + gx;

    sqsum_blob_data[gi] = sqsum;
}
