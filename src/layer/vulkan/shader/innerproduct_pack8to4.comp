// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2020 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
struct sfpvec8 { f16vec4 abcd; f16vec4 efgh; };
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int bias_term = 0;
layout (constant_id = 1) const int activation_type = 0;
layout (constant_id = 2) const float activation_param_0 = 0;
layout (constant_id = 3) const float activation_param_1 = 0;

#define shape_constant_id_offset 4
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler1D bottom_blob;
layout (binding = 1, imfmtc4) writeonly uniform unfp image1D top_blob;
layout (binding = 2) uniform unfp sampler2D weight_blob;
layout (binding = 3) uniform unfp sampler1D bias_blob;
#else
layout (binding = 0) readonly buffer bottom_blob { sfpvec8 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfpvec4 top_blob_data[]; };
layout (binding = 2) readonly buffer weight_blob { sfpvec8 weight_data[]; };
layout (binding = 3) readonly buffer bias_blob { sfpvec4 bias_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= 1 || gz >= 1)
        return;

    afpvec4 sum;

    if (bias_term == 1)
    {
#if NCNN_image_shader
        sum = image1d_ld4(bias_blob, gx);
#else
        sum = buffer_ld4(bias_data, gx);
#endif
    }
    else
    {
        sum = afpvec4(0.f);
    }

#if NCNN_image_shader
    int wx = 0;

    for (int i = 0; i < psc(w); i++)
    {
        afpvec8 v = image1d_ld8(bottom_blob, i);

        afpvec8 k0 = image2d_ld8(weight_blob, ivec2(wx + 0, gx));
        afpvec8 k1 = image2d_ld8(weight_blob, ivec2(wx + 1, gx));
        afpvec8 k2 = image2d_ld8(weight_blob, ivec2(wx + 2, gx));
        afpvec8 k3 = image2d_ld8(weight_blob, ivec2(wx + 3, gx));

        // sum += v * k
        sum.r += dot(v[0], k0[0]) + dot(v[1], k0[1]);
        sum.g += dot(v[0], k1[0]) + dot(v[1], k1[1]);
        sum.b += dot(v[0], k2[0]) + dot(v[1], k2[1]);
        sum.a += dot(v[0], k3[0]) + dot(v[1], k3[1]);

        wx += 4;
    }
#else
    int w_offset = gx * psc(w);

    for (int i = 0; i < psc(w); i++)
    {
        afpvec8 v = buffer_ld8(bottom_blob_data, i);

        afpvec8 k0 = buffer_ld8(weight_data, (w_offset + i) * 4 + 0);
        afpvec8 k1 = buffer_ld8(weight_data, (w_offset + i) * 4 + 1);
        afpvec8 k2 = buffer_ld8(weight_data, (w_offset + i) * 4 + 2);
        afpvec8 k3 = buffer_ld8(weight_data, (w_offset + i) * 4 + 3);

        // sum += v * k
        sum.r += dot(v[0], k0[0]) + dot(v[1], k0[1]);
        sum.g += dot(v[0], k1[0]) + dot(v[1], k1[1]);
        sum.b += dot(v[0], k2[0]) + dot(v[1], k2[1]);
        sum.a += dot(v[0], k3[0]) + dot(v[1], k3[1]);
    }
#endif

    if (activation_type == 1)
    {
        sum = max(sum, afp(0.f));
    }
    if (activation_type == 2)
    {
        const afp slope = afp(activation_param_0);
        sum = mix(sum, sum * afp(slope), lessThan(sum, afpvec4(0.f)));
    }
    if (activation_type == 3)
    {
        const afp const_min = afp(activation_param_0);
        const afp const_max = afp(activation_param_1);
        sum = clamp(sum, const_min, const_max);
    }
    if (activation_type == 4)
    {
        sum = afp(1.f) / (afp(1.f) + exp(-sum));
    }
    if (activation_type == 5)
    {
        sum = sum * tanh(log(exp(sum) + afp(1.f)));
    }

#if NCNN_image_shader
    image1d_st4(top_blob, gx, sum);
#else
    buffer_st4(top_blob_data, gx, sum);
#endif
}
