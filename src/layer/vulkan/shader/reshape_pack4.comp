// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_AMD_gpu_shader_half_float: require
#endif

layout (constant_id = 0) const int ndim = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

layout (binding = 0) readonly buffer bottom_blob { sfpvec4 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfpvec4 top_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.outw || gy >= p.outh || gz >= p.outc)
        return;

    ivec4 i4;

    if (ndim == 1) i4 = gx * 4 + ivec4(0, 1, 2, 3);
    if (ndim == 2) i4 = (gy * 4) * p.outw + gx + ivec4(0, 1, 2, 3) * p.outw;
    if (ndim == 3) i4 = (gz * 4) * p.outh * p.outw + gy * p.outw + gx + ivec4(0, 1, 2, 3) * p.outh * p.outw;

    ivec4 v_offset;
    ivec4 lane4;

    if (p.dims == 1)
    {
        v_offset = i4 / 4;
        lane4 = i4 % 4;
    }
    else if (p.dims == 2)
    {
        ivec4 y4 = i4 / p.w;
        ivec4 x4 = i4 % p.w;

        v_offset = (y4 / 4) * p.w + x4;
        lane4 = y4 % 4;
    }
    else // if (p.dims == 3)
    {
        int size = p.w * p.h;

        ivec4 z4 = i4 / size;
        ivec4 y4 = i4 % size / p.w;
        ivec4 x4 = i4 % size % p.w;

        v_offset = (z4 / 4) * p.cstep + y4 * p.w + x4;
        lane4 = z4 % 4;
    }

    int gi;
    if (ndim == 1)
    {
        gi = gx;
    }
    if (ndim == 2)
    {
        gi = gy * p.outw + gx;
    }
    if (ndim == 3)
    {
        gi = gz * p.outcstep + gy * p.outw + gx;
    }

    if (lane4.r == 0) top_blob_data[gi].r = bottom_blob_data[v_offset.r].r;
    else if (lane4.r == 1) top_blob_data[gi].r = bottom_blob_data[v_offset.r].g;
    else if (lane4.r == 2) top_blob_data[gi].r = bottom_blob_data[v_offset.r].b;
    else /* if (lane4.r == 3) */ top_blob_data[gi].r = bottom_blob_data[v_offset.r].a;

    if (lane4.g == 0) top_blob_data[gi].g = bottom_blob_data[v_offset.g].r;
    else if (lane4.g == 1) top_blob_data[gi].g = bottom_blob_data[v_offset.g].g;
    else if (lane4.g == 2) top_blob_data[gi].g = bottom_blob_data[v_offset.g].b;
    else /* if (lane4.g == 3) */ top_blob_data[gi].g = bottom_blob_data[v_offset.g].a;

    if (lane4.b == 0) top_blob_data[gi].b = bottom_blob_data[v_offset.b].r;
    else if (lane4.b == 1) top_blob_data[gi].b = bottom_blob_data[v_offset.b].g;
    else if (lane4.b == 2) top_blob_data[gi].b = bottom_blob_data[v_offset.b].b;
    else /* if (lane4.b == 3) */ top_blob_data[gi].b = bottom_blob_data[v_offset.b].a;

    if (lane4.a == 0) top_blob_data[gi].a = bottom_blob_data[v_offset.a].r;
    else if (lane4.a == 1) top_blob_data[gi].a = bottom_blob_data[v_offset.a].g;
    else if (lane4.a == 2) top_blob_data[gi].a = bottom_blob_data[v_offset.a].b;
    else /* if (lane4.a == 3) */ top_blob_data[gi].r = bottom_blob_data[v_offset.a].a;
}
