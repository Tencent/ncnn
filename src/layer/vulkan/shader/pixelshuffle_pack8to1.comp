// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2020 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
struct sfpvec8 { f16vec4 abcd; f16vec4 efgh; };
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int upscale_factor = 0;
layout (constant_id = 1) const int mode = 0;

#define shape_constant_id_offset 2
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler3D bottom_blob;
layout (binding = 1, imfmtc1) writeonly uniform unfp image3D top_blob;
#else
layout (binding = 0) readonly buffer bottom_blob { sfpvec8 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfp top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(w) || gy >= psc(h) || gz >= psc(c))
        return;

    ivec4 gz4 = gz * 8 + ivec4(0, 1, 2, 3);
    ivec4 gzz4 = gz4 + 4;

    ivec4 z4;
    ivec4 zi4;
    ivec4 zz4;
    ivec4 zii4;
    if (mode == 0)
    {
        z4 = gz4 / (upscale_factor * upscale_factor);
        zi4 = gz4 % (upscale_factor * upscale_factor);
        zz4 = gzz4 / (upscale_factor * upscale_factor);
        zii4 = gzz4 % (upscale_factor * upscale_factor);
    }
    else // if (mode == 1)
    {
        z4 = gz4 % psc(outc);
        zi4 = gz4 / psc(outc);
        zz4 = gzz4 % psc(outc);
        zii4 = gzz4 / psc(outc);
    }
    ivec4 y4 = gy * upscale_factor + zi4 / upscale_factor;
    ivec4 x4 = gx * upscale_factor + zi4 % upscale_factor;
    ivec4 yy4 = gy * upscale_factor + zii4 / upscale_factor;
    ivec4 xx4 = gx * upscale_factor + zii4 % upscale_factor;

#if NCNN_image_shader
    afpvec8 v = image3d_ld8(bottom_blob, ivec3(gx, gy, gz));

    image3d_st1(top_blob, ivec3(x4.r, y4.r, z4.r), v[0].r);
    image3d_st1(top_blob, ivec3(x4.g, y4.g, z4.g), v[0].g);
    image3d_st1(top_blob, ivec3(x4.b, y4.b, z4.b), v[0].b);
    image3d_st1(top_blob, ivec3(x4.a, y4.a, z4.a), v[0].a);
    image3d_st1(top_blob, ivec3(xx4.r, yy4.r, zz4.r), v[1].r);
    image3d_st1(top_blob, ivec3(xx4.g, yy4.g, zz4.g), v[1].g);
    image3d_st1(top_blob, ivec3(xx4.b, yy4.b, zz4.b), v[1].b);
    image3d_st1(top_blob, ivec3(xx4.a, yy4.a, zz4.a), v[1].a);
#else
    ivec4 v_offset = z4 * psc(outcstep) + y4 * psc(outw) + x4;
    ivec4 vv_offset = zz4 * psc(outcstep) + yy4 * psc(outw) + xx4;

    int gi = gz * psc(cstep) + gy * psc(w) + gx;

    buffer_cp8to1(top_blob_data, v_offset, vv_offset, bottom_blob_data, gi);
#endif
}
