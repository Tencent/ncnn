// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2025 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#extension GL_GOOGLE_include_directive: enable
#include "vulkan_activation.comp"

#extension GL_KHR_memory_scope_semantics: require
#extension GL_EXT_shader_explicit_arithmetic_types: require
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#if ncnn_VK_KHR_cooperative_matrix
#extension GL_KHR_cooperative_matrix: require
#elif ncnn_VK_NV_cooperative_matrix
#extension GL_NV_cooperative_matrix: require
#endif

layout (constant_id = 0) const int bias_term = 0;
layout (constant_id = 1) const int activation_type = 0;
layout (constant_id = 2) const float activation_param_0 = 0;
layout (constant_id = 3) const float activation_param_1 = 0;

layout (constant_id = 4) const int M = 1;
layout (constant_id = 5) const int N = 1;
layout (constant_id = 6) const int K = 1;

layout (constant_id = 7) const int UNROLL_M = 1;
layout (constant_id = 8) const int UNROLL_N = 1;

layout (constant_id = 9) const int elempack = 1;
layout (constant_id = 10) const int out_elempack = 1;

#define shape_constant_id_offset 11
layout (constant_id = shape_constant_id_offset + 0) const int w = 0;
layout (constant_id = shape_constant_id_offset + 1) const int c = 0;
layout (constant_id = shape_constant_id_offset + 2) const int cstep = 0;
layout (constant_id = shape_constant_id_offset + 3) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 4) const int outcstep = 0;

layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfp top_blob_data[]; };
layout (binding = 2) readonly buffer weight_blob { sfp weight_data[]; };
layout (binding = 3) readonly buffer bias_blob { sfp bias_data[]; };

layout (push_constant) uniform parameter
{
    int w;
    int c;
    int cstep;
    int outc;
    int outcstep;
} p;

// #define UNROLL_M 2
// #define UNROLL_N 2
// #define UNROLL_K 4

// MxK
shared uvec2 tmp_k[UNROLL_M][M * K / 4];

// KxN
shared uvec2 tmp_v[UNROLL_N][K * N / 4];

// MxN
shared uvec2 tmp_o[UNROLL_M][UNROLL_N][M * N / 4];

void main()
{
    // FIXME hardcode
//     const uint gi = gl_GlobalInvocationID.x / 64;
    const uint gi = gl_WorkGroupID.x;
    const uint li = gl_LocalInvocationID.x;


    // w = (0 ... w)/M  *  (0 ... c)/K

    // v = (0 ... c)/K  *  (0 ... outc)/N

    // gi = (0 ... w)/M  *  (0 ... outc)/N

    // li = 0 1 2 3 .... 31

    const int mm = (psc(outc) + (M * UNROLL_M) - 1) / (M * UNROLL_M);
    const int nn = (psc(w) + (N * UNROLL_N) - 1) / (N * UNROLL_N);
    const int kk = (psc(c) + K - 1) / K;

    if (gi >= mm * nn)
        return;

    const uint mi = (gi / nn) * (M * UNROLL_M);
    const uint ni = (gi % nn) * (N * UNROLL_N);

#if ncnn_VK_KHR_cooperative_matrix
    coopmat<afp, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator> sum[UNROLL_M][UNROLL_N];
#elif ncnn_VK_NV_cooperative_matrix
#if NCNN_fp16_arithmetic
    fcoopmatNV<16, gl_ScopeSubgroup, M, N> sum[UNROLL_M][UNROLL_N];
#else
    fcoopmatNV<32, gl_ScopeSubgroup, M, N> sum[UNROLL_M][UNROLL_N];
#endif
#endif

    if (bias_term == 1)
    {
        for (int zm = 0; zm < UNROLL_M; zm++)
        {
#if ncnn_VK_KHR_cooperative_matrix
            coopmat<float16_t, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator> bias;
            coopMatLoad(bias, bias_data, mi + zm * M, 0, gl_CooperativeMatrixLayoutColumnMajor);
#elif ncnn_VK_NV_cooperative_matrix
            fcoopmatNV<16, gl_ScopeSubgroup, M, N> bias;
            coopMatLoadNV(bias, bias_data, mi + zm * M, 0, true);
#endif

            for (int zn = 0; zn < UNROLL_N; zn++)
            {
#if NCNN_fp16_arithmetic
                sum[zm][zn] = bias;
#else
#if ncnn_VK_KHR_cooperative_matrix
                sum[zm][zn] = coopmat<float, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator>(bias);
#elif ncnn_VK_NV_cooperative_matrix
                sum[zm][zn] = fcoopmatNV<32, gl_ScopeSubgroup, M, N>(bias);
#endif
#endif
            }
        }
    }
    else
    {
        for (int zm = 0; zm < UNROLL_M; zm++)
        {
            for (int zn = 0; zn < UNROLL_N; zn++)
            {
#if ncnn_VK_KHR_cooperative_matrix
                sum[zm][zn] = coopmat<afp, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator>(0.f);
#elif ncnn_VK_NV_cooperative_matrix
#if NCNN_fp16_arithmetic
                sum[zm][zn] = fcoopmatNV<16, gl_ScopeSubgroup, M, N>(0.f);
#else
                sum[zm][zn] = fcoopmatNV<32, gl_ScopeSubgroup, M, N>(0.f);
#endif
#endif
            }
        }
    }

    for (int k = 0; k < kk; k++)
    {
        const uint ki = k * K;


        if (li == 0)
        {

        //  +-K-+
        //  |   |
        //  M   |
        //  |   |
        //  +---+

        for (int zm = 0; zm < UNROLL_M; zm++)
        {

        // load MxK
        for (int i = 0; i < M; i++)
        {
        for (int j = 0; j < K; j++)
        {
            const uint gm = mi + zm * M + i;
            const uint gk = ki + j;

            afp v = (gm < psc(outc) && gk < psc(c)) ? buffer_ld1(weight_data, gm * psc(c) + gk) : afp(0.f);

            // TODO optimize me
            {
                vec2 v2 = unpackHalf2x16(tmp_k[zm][(i * K + j) / 4][((i * K + j) % 4) / 2]);
                v2[(i * K + j) % 2] = v;
                tmp_k[zm][(i * K + j) / 4][((i * K + j) % 4) / 2] = packHalf2x16(v2);
            }
        }
        }

        }

        //  +-N-+
        //  |   |
        //  K   |
        //  |   |
        //  +---+

        for (int zn = 0; zn < UNROLL_N; zn++)
        {

        // load KxN
        for (int i = 0; i < K; i++)
        {
        for (int j = 0; j < N; j++)
        {
            const uint gk = ki + i;
            const uint gn = ni + zn * N + j;

            afp v = (gk < psc(c) && gn < psc(w)) ? buffer_ld1(bottom_blob_data, gk * psc(cstep) + gn) : afp(0.f);

            // TODO optimize me
            {
                vec2 v2 = unpackHalf2x16(tmp_v[zn][(i * N + j) / 4][((i * N + j) % 4) / 2]);
                v2[(i * N + j) % 2] = v;
                tmp_v[zn][(i * N + j) / 4][((i * N + j) % 4) / 2] = packHalf2x16(v2);
            }
        }
        }

        }

        }

        barrier();

#if ncnn_VK_KHR_cooperative_matrix
        coopmat<float16_t, gl_ScopeSubgroup, M, K, gl_MatrixUseA> A[UNROLL_M];
        coopmat<float16_t, gl_ScopeSubgroup, K, N, gl_MatrixUseB> B[UNROLL_N];
#elif ncnn_VK_NV_cooperative_matrix
        fcoopmatNV<16, gl_ScopeSubgroup, M, K> A[UNROLL_M];
        fcoopmatNV<16, gl_ScopeSubgroup, K, N> B[UNROLL_N];
#endif

        for (int zm = 0; zm < UNROLL_M; zm++)
        {
#if ncnn_VK_KHR_cooperative_matrix
            coopMatLoad(A[zm], tmp_k[zm], 0, K / 4, gl_CooperativeMatrixLayoutRowMajor);
#elif ncnn_VK_NV_cooperative_matrix
            coopMatLoadNV(A[zm], tmp_k[zm], 0, K / 4, false);
#endif
        }

        for (int zn = 0; zn < UNROLL_N; zn++)
        {
#if ncnn_VK_KHR_cooperative_matrix
            coopMatLoad(B[zn], tmp_v[zn], 0, N / 4, gl_CooperativeMatrixLayoutRowMajor);
#elif ncnn_VK_NV_cooperative_matrix
            coopMatLoadNV(B[zn], tmp_v[zn], 0, N / 4, false);
#endif
        }

        // sum += k * v
        for (int zm = 0; zm < UNROLL_M; zm++)
        {
            for (int zn = 0; zn < UNROLL_N; zn++)
            {
#if ncnn_VK_KHR_cooperative_matrix
                sum[zm][zn] = coopMatMulAdd(A[zm], B[zn], sum[zm][zn]);
#elif ncnn_VK_NV_cooperative_matrix
                sum[zm][zn] = coopMatMulAddNV(A[zm], B[zn], sum[zm][zn]);
#endif
            }
        }

        barrier();
    }

    for (int zm = 0; zm < UNROLL_M; zm++)
    {
        for (int zn = 0; zn < UNROLL_N; zn++)
        {
#if ncnn_VK_KHR_cooperative_matrix
#if NCNN_fp16_arithmetic
            coopMatStore(sum[zm][zn], tmp_o[zm][zn], 0, N / 4, gl_CooperativeMatrixLayoutRowMajor);
#else
            coopmat<float16_t, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator> sum_fp16 = coopmat<float16_t, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator>(sum[zm][zn]);
            coopMatStore(sum_fp16, tmp_o[zm][zn], 0, N / 4, gl_CooperativeMatrixLayoutRowMajor);
#endif
#elif ncnn_VK_NV_cooperative_matrix
#if NCNN_fp16_arithmetic
            coopMatStoreNV(sum[zm][zn], tmp_o[zm][zn], 0, N / 4, false);
#else
            fcoopmatNV<16, gl_ScopeSubgroup, M, N> sum_fp16 = fcoopmatNV<16, gl_ScopeSubgroup, M, N>(sum[zm][zn]);
            coopMatStoreNV(sum_fp16, tmp_o[zm][zn], 0, N / 4, false);
#endif
#endif
        }
    }

    barrier();

    if (li == 0)
    {

    //  +-N-+
    //  |   |
    //  M   |
    //  |   |
    //  +---+

    for (int zm = 0; zm < UNROLL_M; zm++)
    {
        for (int zn = 0; zn < UNROLL_N; zn++)
        {

            // store MxN
            for (int i = 0; i < M; i++)
            {
            for (int j = 0; j < N; j++)
            {
                const uint gm = mi + zm * M + i;
                const uint gn = ni + zn * N + j;

                if (gm < psc(outc) && gn < psc(w))
                {
                    // TODO optimize me

                    afp v = afp(unpackHalf2x16(tmp_o[zm][zn][(i * N + j) / 4][((i * N + j) % 4) / 2])[(i * N + j) % 2]);

                    v = activation_afp(v, activation_type, activation_param_0, activation_param_1);

                    buffer_st1(top_blob_data, gm * psc(outcstep) + gn, v);
                }
            }
            }

        }
    }

    }
}
