// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2025 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#extension GL_GOOGLE_include_directive: require
#include "vulkan_activation.comp"

#extension GL_EXT_control_flow_attributes: require

#extension GL_KHR_memory_scope_semantics: require
#extension GL_EXT_shader_explicit_arithmetic_types: require
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#if ncnn_VK_KHR_cooperative_matrix
#extension GL_KHR_cooperative_matrix: require
#elif ncnn_VK_NV_cooperative_matrix
#extension GL_NV_cooperative_matrix: require
#endif

layout (constant_id = 0) const int bias_term = 0;
layout (constant_id = 1) const int activation_type = 0;
layout (constant_id = 2) const float activation_param_0 = 0;
layout (constant_id = 3) const float activation_param_1 = 0;
layout (constant_id = 4) const uint M = 1;
layout (constant_id = 5) const uint N = 1;
layout (constant_id = 6) const uint K = 1;
layout (constant_id = 7) const uint UNROLL_M = 1;
layout (constant_id = 8) const uint UNROLL_N = 1;
layout (constant_id = 9) const uint inch = 1;
layout (constant_id = 10) const uint outch = 1;
layout (constant_id = 11) const uint elempack = 1;
layout (constant_id = 12) const uint out_elempack = 1;

#define shape_constant_id_offset 13
layout (constant_id = shape_constant_id_offset + 0) const int size = 0;
layout (constant_id = shape_constant_id_offset + 1) const int cstep = 0;
layout (constant_id = shape_constant_id_offset + 2) const int outcstep = 0;

layout (binding = 0) readonly buffer bottom_blob { uvec2 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfpvec4 top_blob_data[]; };
layout (binding = 2) readonly buffer weight_blob { uvec2 weight_data[]; };
layout (binding = 3) readonly buffer bias_blob { uvec2 bias_data[]; };

layout (push_constant) uniform parameter
{
    int size;
    int cstep;
    int outcstep;
} p;

// MxK
shared uvec2 tmp_v[UNROLL_M][M * K / 4];

// KxN
shared uvec2 tmp_k[UNROLL_N][K * N / 4];

// MxN
shared uvec2 tmp_o[UNROLL_N][UNROLL_M][M * N / 4];

void main()
{
    const uint gi = gl_WorkGroupID.x;
    const uint li = gl_LocalInvocationID.x;

    const uint mm = (psc(size) + (M * UNROLL_M) - 1) / (M * UNROLL_M);
    const uint nn = (outch + (N * UNROLL_N) - 1) / (N * UNROLL_N);
    const uint kk = (inch + K - 1) / K;

    if (gi >= mm * nn)
        return;

    // assert gl_WorkGroupSize.x == gl_SubgroupSize
    // but neither gl_SubgroupSize nor gl_WorkGroupSize.x is a constant
    const uint subgroup_size = ncnn_subgroupSize;

    const uint ni = (gi / mm) * (N * UNROLL_N);
    const uint mi = (gi % mm) * (M * UNROLL_M);

#if ncnn_VK_KHR_cooperative_matrix
    coopmat<afp, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator> sum[UNROLL_N][UNROLL_M];
#elif ncnn_VK_NV_cooperative_matrix
#if NCNN_fp16_arithmetic
    fcoopmatNV<16, gl_ScopeSubgroup, M, N> sum[UNROLL_N][UNROLL_M];
#else
    fcoopmatNV<32, gl_ScopeSubgroup, M, N> sum[UNROLL_N][UNROLL_M];
#endif
#endif

    if (bias_term == 1)
    {
        [[unroll]] for (uint zn = 0; zn < UNROLL_N; zn++)
        {
#if ncnn_VK_KHR_cooperative_matrix
            coopmat<float16_t, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator> bias;
            coopMatLoad(bias, bias_data, (ni + zn * N) / 4, 0, gl_CooperativeMatrixLayoutRowMajor);
#elif ncnn_VK_NV_cooperative_matrix
            fcoopmatNV<16, gl_ScopeSubgroup, M, N> bias;
            coopMatLoadNV(bias, bias_data, (ni + zn * N) / 4, 0, false);
#endif

#if !NCNN_fp16_arithmetic
#if ncnn_VK_KHR_cooperative_matrix
            coopmat<float, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator> bias_fp32 = coopmat<float, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator>(bias);
#elif ncnn_VK_NV_cooperative_matrix
            fcoopmatNV<32, gl_ScopeSubgroup, M, N> bias_fp32 = fcoopmatNV<32, gl_ScopeSubgroup, M, N>(bias);
#endif
#endif

            [[unroll]] for (uint zm = 0; zm < UNROLL_M; zm++)
            {
#if NCNN_fp16_arithmetic
                sum[zn][zm] = bias;
#else
                sum[zn][zm] = bias_fp32;
#endif
            }
        }
    }
    else
    {
        [[unroll]] for (uint zn = 0; zn < UNROLL_N; zn++)
        {
            [[unroll]] for (uint zm = 0; zm < UNROLL_M; zm++)
            {
#if ncnn_VK_KHR_cooperative_matrix
                sum[zn][zm] = coopmat<afp, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator>(0.f);
#elif ncnn_VK_NV_cooperative_matrix
#if NCNN_fp16_arithmetic
                sum[zn][zm] = fcoopmatNV<16, gl_ScopeSubgroup, M, N>(0.f);
#else
                sum[zn][zm] = fcoopmatNV<32, gl_ScopeSubgroup, M, N>(0.f);
#endif
#endif
            }
        }
    }

    for (uint k = 0; k < kk; k++)
    {
        const uint ki = k * K;

        // load bottom_blob
        {
            if (elempack == 1)
            {
                //  +-M-+
                //  |   |
                //  K   |
                //  |   |
                //  +---+

                const uint Md4 = M / 4;

                const uint UNROLL_M_K_Md4_d_localsize = (UNROLL_M * K * Md4 + subgroup_size - 1) / subgroup_size;
                [[unroll]] for (uint q = 0; q < UNROLL_M_K_Md4_d_localsize; q++)
                {
                    const uint liq = li + q * subgroup_size;

                    const uint zm = liq / (K * Md4);
                    const uint ij = liq % (K * Md4);
                    const uint i = ij / Md4;
                    const uint j = ij % Md4;

                    if (zm < UNROLL_M && i < K)
                    {
                        const uint gk = ki + i;
                        const uint gm = mi + zm * M + j * 4;

                        uvec2 v = gk < inch ? bottom_blob_data[(gk * psc(cstep) + gm) / 4] : uvec2(0);

                        tmp_v[zm][i * Md4 + j] = v;
                    }
                }
            }
            else // if (elempack == 4)
            {
                //  +-K-+
                //  |   |
                //  M   |
                //  |   |
                //  +---+

                const uint Kd4 = K / 4;

                const uint UNROLL_M_M_Kd4_d_localsize = (UNROLL_M * M * Kd4 + subgroup_size - 1) / subgroup_size;
                [[unroll]] for (uint q = 0; q < UNROLL_M_M_Kd4_d_localsize; q++)
                {
                    const uint liq = li + q * subgroup_size;

                    const uint zm = liq / (M * Kd4);
                    const uint ij = liq % (M * Kd4);
                    const uint i = ij / Kd4;
                    const uint j = ij % Kd4;

                    if (zm < UNROLL_M && i < M)
                    {
                        const uint gm = mi + zm * M + i;
                        const uint gk = ki / 4 + j;

                        uvec2 v = gk < inch / 4 ? bottom_blob_data[gk * psc(cstep) + gm] : uvec2(0);

                        tmp_v[zm][i * Kd4 + j] = v;
                    }
                }
            }
        }

        // load weight
        {
            //  +-N-+
            //  |   |
            //  K   |
            //  |   |
            //  +---+

            const uint Nd4 = N / 4;
            const uint outch_p4d4 = (outch + 3) / 4 * 4;

            const uint UNROLL_N_K_Nd4_d_localsize = (UNROLL_N * K * Nd4 + subgroup_size - 1) / subgroup_size;
            [[unroll]] for (uint q = 0; q < UNROLL_N_K_Nd4_d_localsize; q++)
            {
                const uint liq = li + q * subgroup_size;

                const uint zn = liq / (K * Nd4);
                const uint ij = liq % (K * Nd4);
                const uint i = ij / Nd4;
                const uint j = ij % Nd4;

                if (zn < UNROLL_N && i < K)
                {
                    const uint gk = ki + i;
                    const uint gn = ni + zn * N + j * 4;

                    uvec2 v = gk < inch ? weight_data[(gk * outch_p4d4 + gn) / 4] : uvec2(0);

                    tmp_k[zn][i * Nd4 + j] = v;
                }
            }
        }

        barrier();

#if ncnn_VK_KHR_cooperative_matrix
        coopmat<float16_t, gl_ScopeSubgroup, M, K, gl_MatrixUseA> A[UNROLL_M];
        coopmat<float16_t, gl_ScopeSubgroup, K, N, gl_MatrixUseB> B[UNROLL_N];
#elif ncnn_VK_NV_cooperative_matrix
        fcoopmatNV<16, gl_ScopeSubgroup, M, K> A[UNROLL_M];
        fcoopmatNV<16, gl_ScopeSubgroup, K, N> B[UNROLL_N];
#endif

        [[unroll]] for (uint zm = 0; zm < UNROLL_M; zm++)
        {
            const uint A_stride = elempack == 1 ? K / 4 : N / 4;
#if ncnn_VK_KHR_cooperative_matrix
            const int A_layout = elempack == 1 ? gl_CooperativeMatrixLayoutColumnMajor : gl_CooperativeMatrixLayoutRowMajor;
            coopMatLoad(A[zm], tmp_v[zm], 0, A_stride, A_layout);
#elif ncnn_VK_NV_cooperative_matrix
            const bool A_colmajor = elempack == 1 ? true : false;
            coopMatLoadNV(A[zm], tmp_v[zm], 0, A_stride, A_colmajor);
#endif
        }

        [[unroll]] for (uint zn = 0; zn < UNROLL_N; zn++)
        {
#if ncnn_VK_KHR_cooperative_matrix
            coopMatLoad(B[zn], tmp_k[zn], 0, N / 4, gl_CooperativeMatrixLayoutRowMajor);
#elif ncnn_VK_NV_cooperative_matrix
            coopMatLoadNV(B[zn], tmp_k[zn], 0, N / 4, false);
#endif
        }

        // sum += k * v
        [[unroll]] for (uint zn = 0; zn < UNROLL_N; zn++)
        {
            [[unroll]] for (uint zm = 0; zm < UNROLL_M; zm++)
            {
#if ncnn_VK_KHR_cooperative_matrix
                sum[zn][zm] = coopMatMulAdd(A[zm], B[zn], sum[zn][zm]);
#elif ncnn_VK_NV_cooperative_matrix
                sum[zn][zm] = coopMatMulAddNV(A[zm], B[zn], sum[zn][zm]);
#endif
            }
        }

        barrier();
    }

    [[unroll]] for (uint zn = 0; zn < UNROLL_N; zn++)
    {
        [[unroll]] for (uint zm = 0; zm < UNROLL_M; zm++)
        {
            const uint sum_stride = out_elempack == 1 ? M / 4 : N / 4;
#if ncnn_VK_KHR_cooperative_matrix
            const int sum_layout = out_elempack == 1 ? gl_CooperativeMatrixLayoutColumnMajor : gl_CooperativeMatrixLayoutRowMajor;
#if NCNN_fp16_arithmetic
            coopMatStore(sum[zn][zm], tmp_o[zn][zm], 0, sum_stride, sum_layout);
#else
            coopmat<float16_t, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator> sum_fp16 = coopmat<float16_t, gl_ScopeSubgroup, M, N, gl_MatrixUseAccumulator>(sum[zn][zm]);
            coopMatStore(sum_fp16, tmp_o[zn][zm], 0, sum_stride, sum_layout);
#endif
#elif ncnn_VK_NV_cooperative_matrix
            const bool sum_colmajor = out_elempack == 1 ? true : false;
#if NCNN_fp16_arithmetic
            coopMatStoreNV(sum[zn][zm], tmp_o[zn][zm], 0, sum_stride, sum_colmajor);
#else
            fcoopmatNV<16, gl_ScopeSubgroup, M, N> sum_fp16 = fcoopmatNV<16, gl_ScopeSubgroup, M, N>(sum[zn][zm]);
            coopMatStoreNV(sum_fp16, tmp_o[zn][zm], 0, sum_stride, sum_colmajor);
#endif
#endif
        }
    }

    barrier();

    // store top_blob
    {
        if (out_elempack == 1)
        {
            //  +-M-+
            //  |   |
            //  N   |
            //  |   |
            //  +---+

            const uint Md4 = M / 4;

            const uint UNROLL_NM_N_Md4_d_localsize = (UNROLL_N * UNROLL_M * N * Md4 + subgroup_size - 1) / subgroup_size;
            [[unroll]] for (uint q = 0; q < UNROLL_NM_N_Md4_d_localsize; q++)
            {
                const uint liq = li + q * subgroup_size;

                const uint zn = liq / (UNROLL_M * N * Md4);
                const uint zmij = liq % (UNROLL_M * N * Md4);
                const uint zm = zmij / (N * Md4);
                const uint ij = zmij % (N * Md4);
                const uint i = ij / Md4;
                const uint j = ij % Md4;

                const uint gn = ni + zn * N + i;
                const uint gm = mi + zm * M + j * 4;

                if (zn < UNROLL_N && zm < UNROLL_M && i < N && gn < outch && gm < psc(size))
                {
                    uvec2 sum = tmp_o[zn][zm][i * Md4 + j];

                    afpvec4 v = afpvec4(unpackHalf2x16(sum.r), unpackHalf2x16(sum.g));

                    v = activation_afpvec4(v, activation_type, activation_param_0, activation_param_1);

                    buffer_st4(top_blob_data, (gn * psc(outcstep) + gm) / 4, v);
                }
            }
        }
        else // if (out_elempack == 4)
        {
            //  +-N-+
            //  |   |
            //  M   |
            //  |   |
            //  +---+

            const uint Nd4 = N / 4;

            const uint UNROLL_NM_M_Nd4_d_localsize = (UNROLL_N * UNROLL_M * M * Nd4 + subgroup_size - 1) / subgroup_size;
            [[unroll]] for (uint q = 0; q < UNROLL_NM_M_Nd4_d_localsize; q++)
            {
                const uint liq = li + q * subgroup_size;

                const uint zn = liq / (UNROLL_M * M * Nd4);
                const uint zmij = liq % (UNROLL_M * M * Nd4);
                const uint zm = zmij / (M * Nd4);
                const uint ij = zmij % (M * Nd4);
                const uint i = ij / Nd4;
                const uint j = ij % Nd4;

                const uint gm = mi + zm * M + i;
                const uint gn = (ni + zn * N) / 4 + j;

                if (zn < UNROLL_N && zm < UNROLL_M && i < M && gn < outch / 4 && gm < psc(size))
                {
                    uvec2 sum = tmp_o[zn][zm][i * Nd4 + j];

                    afpvec4 v = afpvec4(unpackHalf2x16(sum.r), unpackHalf2x16(sum.g));

                    v = activation_afpvec4(v, activation_type, activation_param_0, activation_param_1);

                    buffer_st4(top_blob_data, gn * psc(outcstep) + gm, v);
                }
            }
        }
    }
}
