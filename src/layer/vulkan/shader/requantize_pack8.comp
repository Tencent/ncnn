// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2022 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_int8_storage
#extension GL_EXT_shader_8bit_storage: require
struct sintvec8 { i8vec4 abcd; i8vec4 efgh; };
struct aintvec8 { ivec4 abcd; ivec4 efgh; };
#elif NCNN_int8_packed
struct aintvec8 { ivec4 abcd; ivec4 efgh; };
#else
struct sintvec8 { vec4 abcd; vec4 efgh; };
struct aintvec8 { ivec4 abcd; ivec4 efgh; };
#endif
#if NCNN_int8_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_int8: require
#endif

#extension GL_GOOGLE_include_directive: enable
#include "vulkan_activation.comp"

layout (constant_id = 0) const int scale_in_data_size = 0;
layout (constant_id = 1) const float scale_in_value = 1.f;
layout (constant_id = 2) const int scale_out_data_size = 0;
layout (constant_id = 3) const float scale_out_value = 1.f;
layout (constant_id = 4) const int bias_data_size = 0;
layout (constant_id = 5) const float bias_value = 0.f;
layout (constant_id = 6) const int activation_type = 0;
layout (constant_id = 7) const float activation_param_0 = 0;
layout (constant_id = 8) const float activation_param_1 = 0;

#define shape_constant_id_offset 9
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

layout (binding = 0) readonly buffer bottom_blob { ivec4 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sintvec8 top_blob_data[]; };
layout (binding = 2) readonly buffer scale_in_blob { sfpvec8 scale_in_blob_data[]; };
layout (binding = 3) readonly buffer scale_out_blob { sfpvec8 scale_out_blob_data[]; };
layout (binding = 4) readonly buffer bias_blob { sfpvec8 bias_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    const int gi = gz * psc(cstep) + gy * psc(w) + gx;

    ivec4 v0 = bottom_blob_data[gi * 2];
    ivec4 v1 = bottom_blob_data[gi * 2 + 1];

    afpvec8 scale_in;

    if (scale_in_data_size == 1)
    {
        scale_in = afpvec8(afpvec4(scale_in_value), afpvec4(scale_in_value));
    }
    else
    {
        const ivec3 gxyz = ivec3(gx, gy, gz);
        const int abi = gxyz[psc(dims) - 1];

        scale_in = buffer_ld8(scale_in_blob_data, abi);
    }

    afpvec8 v_fp;

    if (bias_data_size == 0)
    {
        v_fp[0] = afpvec4(v0) * scale_in[0];
        v_fp[1] = afpvec4(v1) * scale_in[1];
    }
    else if (bias_data_size == 1)
    {
        v_fp[0] = afpvec4(v0) * scale_in[0] + afpvec4(bias_value);
        v_fp[1] = afpvec4(v1) * scale_in[1] + afpvec4(bias_value);
    }
    else
    {
        const ivec3 gxyz = ivec3(gx, gy, gz);
        const int abi = gxyz[psc(dims) - 1];

        afpvec8 bias = buffer_ld8(bias_blob_data, abi);

        v_fp[0] = afpvec4(v0) * scale_in[0] + bias[0];
        v_fp[1] = afpvec4(v1) * scale_in[1] + bias[1];
    }

    v_fp = activation_afpvec8(v_fp, activation_type, activation_param_0, activation_param_1);

    afpvec8 scale_out;

    if (scale_out_data_size == 1)
    {
        scale_out = afpvec8(afpvec4(scale_out_value), afpvec4(scale_out_value));
    }
    else
    {
        const ivec3 gxyz = ivec3(gx, gy, gz);
        const int abi = gxyz[psc(dims) - 1];

        scale_out = buffer_ld8(scale_out_blob_data, abi);
    }

    aintvec8 v_int;
    v_int.abcd = aintvec4(round(clamp(v_fp[0] * scale_out[0], afp(-127.f), afp(127.f))));
    v_int.efgh = aintvec4(round(clamp(v_fp[1] * scale_out[1], afp(-127.f), afp(127.f))));

    const int outgi = gz * psc(outcstep) + gy * psc(outw) + gx;

    ibuffer_st8(top_blob_data, outgi, v_int);
}
