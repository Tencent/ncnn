// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_AMD_gpu_shader_half_float: require
#endif

layout (constant_id = 0) const int region_type = 0;
layout (constant_id = 1) const int pad_head = 0;
layout (constant_id = 2) const int pad_tail = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

layout (binding = 0) readonly buffer bottom_blob { sfpvec4 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer square_workspace { sfp square_workspace_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.outw || gy >= p.outh || gz >= p.outc)
        return;

    // support region_type == 0 only

    afp res;

    int z = (gz - pad_head) / 4;

    if (z >= 0 && z < p.c)
    {
        int v_offset = z * p.cstep + gy * p.w + gx;
        afpvec4 v4 = afpvec4(bottom_blob_data[v_offset]);

        int lane = (gz - pad_head) % 4;

        // v = v4[lane]
        afp v;
        if (lane == 0) v = v4.r;
        else if (lane == 1) v = v4.g;
        else if (lane == 2) v = v4.b;
        else /* if (lane == 3) */ v = v4.a;

        res = v * v;
    }
    else
    {
        res = afp(0.f);
    }

    square_workspace_data[gz * p.outcstep + gy * p.outw + gx] = sfp(res);
}
