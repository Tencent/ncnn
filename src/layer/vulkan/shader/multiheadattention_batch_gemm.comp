// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2023 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

#define LOCAL_MEMORY_UNROLL_INCH 8

layout (constant_id = 0) const int transA = 0;
layout (constant_id = 1) const int transB = 0;
layout (constant_id = 2) const int output_transpose = 0;
layout (constant_id = 3) const int M = 0;
layout (constant_id = 4) const int N = 0;
layout (constant_id = 5) const int K = 0;
layout (constant_id = 6) const int B = 0;

// TODO psc more

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler3D A_blob_3d;
layout (binding = 1) uniform unfp sampler3D B_blob_3d;
layout (binding = 2, imfmtc1) writeonly uniform unfp image3D top_blob_3d;
#else
layout (binding = 0) readonly buffer A_blob { sfp A_blob_data[]; };
layout (binding = 1) readonly buffer B_blob { sfp B_blob_data[]; };
layout (binding = 2) writeonly buffer top_blob { sfp top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int M;
    int N;
    int K;
    int B;
} p;

#if NCNN_shader_local_memory
shared lfp tmp_a[8][LOCAL_MEMORY_UNROLL_INCH][2];
shared lfp tmp_b[8][LOCAL_MEMORY_UNROLL_INCH][2];
#endif

void main()
{
    int gx = int(gl_GlobalInvocationID.x) * 2;
    int gy = int(gl_GlobalInvocationID.y) * 2;
    int gz = int(gl_GlobalInvocationID.z);

#if !NCNN_shader_local_memory
    if (gx >= psc(N) || gy >= psc(M) || gz >= psc(B))
        return;
#endif

    afp sum0 = afp(0.f);
    afp sum1 = afp(0.f);
    afp sum2 = afp(0.f);
    afp sum3 = afp(0.f);

#if !NCNN_image_shader && NCNN_shader_local_memory
    const int NN = psc(K);

    const int lx = int(gl_LocalInvocationID.x);
    const int ly = int(gl_LocalInvocationID.y);

    int k = 0;
    for (; k + (LOCAL_MEMORY_UNROLL_INCH - 1) < NN; k += LOCAL_MEMORY_UNROLL_INCH)
    {
        {
            if (transA == 1)
            {
                const int ai = gz * psc(M) * psc(K) + (k + lx) * psc(M) + gy;
                tmp_a[ly][lx][0] = sfp2lfp(A_blob_data[ai]);
                tmp_a[ly][lx][1] = sfp2lfp(A_blob_data[ai + 1]);
            }
            else
            {
                const int ai = gz * psc(M) * psc(K) + gy * psc(K) + (k + lx);
                tmp_a[ly][lx][0] = sfp2lfp(A_blob_data[ai]);
                tmp_a[ly][lx][1] = sfp2lfp(A_blob_data[ai + psc(K)]);
            }

            if (transB == 1)
            {
                const int bi = gz * psc(N) * psc(K) + gx * psc(K) + (k + ly);
                tmp_b[lx][ly][0] = sfp2lfp(B_blob_data[bi]);
                tmp_b[lx][ly][1] = sfp2lfp(B_blob_data[bi + psc(K)]);
            }
            else
            {
                const int bi = gz * psc(N) * psc(K) + (k + ly) * psc(N) + gx;
                tmp_b[lx][ly][0] = sfp2lfp(B_blob_data[bi]);
                tmp_b[lx][ly][1] = sfp2lfp(B_blob_data[bi + 1]);
            }
        }

        barrier();

        for (int k4 = 0; k4 < LOCAL_MEMORY_UNROLL_INCH; k4++)
        {
            afp a0 = lfp2afp(tmp_a[ly][k4][0]);
            afp a1 = lfp2afp(tmp_a[ly][k4][1]);

            afp b0 = lfp2afp(tmp_b[lx][k4][0]);
            afp b1 = lfp2afp(tmp_b[lx][k4][1]);

            sum0 += a0 * b0;
            sum1 += a0 * b1;
            sum2 += a1 * b0;
            sum3 += a1 * b1;
        }

        barrier();
    }

    if (k < NN)
    {
        const int remain = NN - k;

        if (lx < remain)
        {
            if (transA == 1)
            {
                const int ai = gz * psc(M) * psc(K) + (k + lx) * psc(M) + gy;
                tmp_a[ly][lx][0] = sfp2lfp(A_blob_data[ai]);
                tmp_a[ly][lx][1] = sfp2lfp(A_blob_data[ai + 1]);
            }
            else
            {
                const int ai = gz * psc(M) * psc(K) + gy * psc(K) + (k + lx);
                tmp_a[ly][lx][0] = sfp2lfp(A_blob_data[ai]);
                tmp_a[ly][lx][1] = sfp2lfp(A_blob_data[ai + psc(K)]);
            }
        }

        if (ly < remain)
        {
            if (transB == 1)
            {
                const int bi = gz * psc(N) * psc(K) + gx * psc(K) + (k + ly);
                tmp_b[lx][ly][0] = sfp2lfp(B_blob_data[bi]);
                tmp_b[lx][ly][1] = sfp2lfp(B_blob_data[bi + psc(K)]);
            }
            else
            {
                const int bi = gz * psc(N) * psc(K) + (k + ly) * psc(N) + gx;
                tmp_b[lx][ly][0] = sfp2lfp(B_blob_data[bi]);
                tmp_b[lx][ly][1] = sfp2lfp(B_blob_data[bi + 1]);
            }
        }

        barrier();

        for (int k4 = 0; k4 < remain; k4++)
        {
            afp a0 = lfp2afp(tmp_a[ly][k4][0]);
            afp a1 = lfp2afp(tmp_a[ly][k4][1]);

            afp b0 = lfp2afp(tmp_b[lx][k4][0]);
            afp b1 = lfp2afp(tmp_b[lx][k4][1]);

            sum0 += a0 * b0;
            sum1 += a0 * b1;
            sum2 += a1 * b0;
            sum3 += a1 * b1;
        }
    }
#else
    for (int k = 0; k < psc(K); k++)
    {
        afp a0;
        afp a1;
        afp b0;
        afp b1;
#if NCNN_image_shader
        if (transA == 1)
        {
            a0 = image3d_ld1(A_blob_3d, ivec3(gy, gz * psc(K) + k, 0));
            a1 = image3d_ld1(A_blob_3d, ivec3(gy + 1, gz * psc(K) + k, 0));
        }
        else
        {
            a0 = image3d_ld1(A_blob_3d, ivec3(k, gz * psc(M) + gy, 0));
            a1 = image3d_ld1(A_blob_3d, ivec3(k, gz * psc(M) + gy + 1, 0));
        }

        if (transB == 1)
        {
            b0 = image3d_ld1(B_blob_3d, ivec3(k, gz * psc(N) + gx, 0));
            b1 = image3d_ld1(B_blob_3d, ivec3(k, gz * psc(N) + gx + 1, 0));
        }
        else
        {
            b0 = image3d_ld1(B_blob_3d, ivec3(gx, gz * psc(K) + k, 0));
            b1 = image3d_ld1(B_blob_3d, ivec3(gx + 1, gz * psc(K) + k, 0));
        }
#else
        if (transA == 1)
        {
            const int ai = gz * psc(M) * psc(K) + k * psc(M) + gy;
            a0 = buffer_ld1(A_blob_data, ai);
            a1 = buffer_ld1(A_blob_data, ai + 1);
        }
        else
        {
            const int ai = gz * psc(M) * psc(K) + gy * psc(K) + k;
            a0 = buffer_ld1(A_blob_data, ai);
            a1 = buffer_ld1(A_blob_data, ai + psc(K));
        }

        if (transB == 1)
        {
            const int bi = gz * psc(N) * psc(K) + gx * psc(K) + k;
            b0 = buffer_ld1(B_blob_data, bi);
            b1 = buffer_ld1(B_blob_data, bi + psc(K));
        }
        else
        {
            const int bi = gz * psc(N) * psc(K) + k * psc(N) + gx;
            b0 = buffer_ld1(B_blob_data, bi);
            b1 = buffer_ld1(B_blob_data, bi + 1);
        }
#endif

        sum0 += a0 * b0;
        sum1 += a0 * b1;
        sum2 += a1 * b0;
        sum3 += a1 * b1;
    }
#endif

#if NCNN_shader_local_memory
    if (gx >= psc(N) || gy >= psc(M) || gz >= psc(B))
        return;
#endif

#if NCNN_image_shader
    if (output_transpose == 1)
    {
        image3d_st1(top_blob_3d, ivec3(gy, gz * psc(N) + gx, 0), sum0);
        if (gy + 1 < psc(M)) image3d_st1(top_blob_3d, ivec3(gy + 1, gz * psc(N) + gx, 0), sum2);
        if (gx + 1 < psc(N))
        {
            image3d_st1(top_blob_3d, ivec3(gy, gz * psc(N) + gx + 1, 0), sum1);
            if (gy + 1 < psc(M)) image3d_st1(top_blob_3d, ivec3(gy + 1, gz * psc(N) + gx + 1, 0), sum3);
        }
    }
    else
    {
        image3d_st1(top_blob_3d, ivec3(gx, gz * psc(M) + gy, 0), sum0);
        if (gx + 1 < psc(N)) image3d_st1(top_blob_3d, ivec3(gx + 1, gz * psc(M) + gy, 0), sum1);
        if (gy + 1 < psc(M))
        {
            image3d_st1(top_blob_3d, ivec3(gx, gz * psc(M) + gy + 1, 0), sum2);
            if (gx + 1 < psc(N)) image3d_st1(top_blob_3d, ivec3(gx + 1, gz * psc(M) + gy + 1, 0), sum3);
        }
    }
#else
    if (output_transpose == 1)
    {
        const int gi = gz * psc(M) * psc(N) + gx * psc(M) + gy;

        buffer_st1(top_blob_data, gi, sum0);
        if (gy + 1 < psc(M)) buffer_st1(top_blob_data, gi + 1, sum2);
        if (gx + 1 < psc(N))
        {
            buffer_st1(top_blob_data, gi + psc(M), sum1);
            if (gy + 1 < psc(M)) buffer_st1(top_blob_data, gi + psc(M) + 1, sum3);
        }
    }
    else
    {
        const int gi = gz * psc(M) * psc(N) + gy * psc(N) + gx;

        buffer_st1(top_blob_data, gi, sum0);
        if (gx + 1 < psc(N)) buffer_st1(top_blob_data, gi + 1, sum1);
        if (gy + 1 < psc(M))
        {
            buffer_st1(top_blob_data, gi + psc(N), sum2);
            if (gx + 1 < psc(N)) buffer_st1(top_blob_data, gi + psc(N) + 1, sum3);
        }
    }
#endif
}
