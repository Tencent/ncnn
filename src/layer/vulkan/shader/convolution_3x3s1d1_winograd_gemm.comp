// Copyright 2022 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

#define LOCAL_MEMORY_UNROLL_INCH 8
#define LOCAL_MEMORY_PAD         1

layout(constant_id = 0) const int batch = 1;
layout(constant_id = 1) const int c = 0;
layout(constant_id = 2) const int outc = 0;

#define shape_constant_id_offset 3
layout(constant_id = shape_constant_id_offset + 0) const int cstep = 0;
layout(constant_id = shape_constant_id_offset + 1) const int outw = 0;
layout(constant_id = shape_constant_id_offset + 2) const int outcstep = 0;

layout(binding = 0) readonly buffer bottom_tm_blob { sfp bottom_tm_blob_data[]; };
layout(binding = 1) writeonly buffer top_tm_blob { sfp top_tm_blob_data[]; };
layout(binding = 2) readonly buffer weight_tm_blob { sfp weight_tm_data[]; };

layout(push_constant) uniform parameter
{
    int cstep;
    int outw;
    int outcstep;
} p;

#if NCNN_shader_local_memory
shared lfp tmp_v[8][LOCAL_MEMORY_UNROLL_INCH][4 + LOCAL_MEMORY_PAD];
shared lfp tmp_k[8][LOCAL_MEMORY_UNROLL_INCH + LOCAL_MEMORY_PAD];
#endif

void main()
{
    int gx = int(gl_GlobalInvocationID.x) * 4;
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

#if !NCNN_shader_local_memory
    if (gx >= psc(outw) || gy >= outc || gz >= batch)
        return;
#endif

    afpvec4 sum = afpvec4(0.f);

    int v_offset = gz * c * psc(cstep) + gx;
    int w_offset = gz * c * outc + gy * c;

#if NCNN_shader_local_memory
    const int lx = int(gl_LocalInvocationID.x);
    const int ly = int(gl_LocalInvocationID.y);

    int z = 0;
    for (; z + (LOCAL_MEMORY_UNROLL_INCH - 1) < c; z += LOCAL_MEMORY_UNROLL_INCH)
    {
        if (ly < 4)
        {
            for (int z4 = 0; z4 < LOCAL_MEMORY_UNROLL_INCH; z4++)
            {
                tmp_v[lx][z4][ly] = buffer_sm1(bottom_tm_blob_data, v_offset + z4 * psc(cstep) + ly);
            }
        }

        if (lx == 0)
        {
            for (int z4 = 0; z4 < LOCAL_MEMORY_UNROLL_INCH; z4++)
            {
                tmp_k[ly][z4] = buffer_sm1(weight_tm_data, w_offset + z4);
            }
        }

        barrier();

        int z4 = 0;
        for (; z4 + 3 < LOCAL_MEMORY_UNROLL_INCH; z4 += 4)
        {
            afp k0 = lfp2afp(tmp_k[ly][z4 + 0]);
            afp k1 = lfp2afp(tmp_k[ly][z4 + 1]);
            afp k2 = lfp2afp(tmp_k[ly][z4 + 2]);
            afp k3 = lfp2afp(tmp_k[ly][z4 + 3]);

            afpvec4 v0 = afpvec4(
                lfp2afp(tmp_v[lx][z4 + 0][0]),
                lfp2afp(tmp_v[lx][z4 + 0][1]),
                lfp2afp(tmp_v[lx][z4 + 0][2]),
                lfp2afp(tmp_v[lx][z4 + 0][3]));
            afpvec4 v1 = afpvec4(
                lfp2afp(tmp_v[lx][z4 + 1][0]),
                lfp2afp(tmp_v[lx][z4 + 1][1]),
                lfp2afp(tmp_v[lx][z4 + 1][2]),
                lfp2afp(tmp_v[lx][z4 + 1][3]));
            afpvec4 v2 = afpvec4(
                lfp2afp(tmp_v[lx][z4 + 2][0]),
                lfp2afp(tmp_v[lx][z4 + 2][1]),
                lfp2afp(tmp_v[lx][z4 + 2][2]),
                lfp2afp(tmp_v[lx][z4 + 2][3]));
            afpvec4 v3 = afpvec4(
                lfp2afp(tmp_v[lx][z4 + 3][0]),
                lfp2afp(tmp_v[lx][z4 + 3][1]),
                lfp2afp(tmp_v[lx][z4 + 3][2]),
                lfp2afp(tmp_v[lx][z4 + 3][3]));

            sum += v0 * k0;
            sum += v1 * k1;
            sum += v2 * k2;
            sum += v3 * k3;
        }

        for (; z4 < LOCAL_MEMORY_UNROLL_INCH; z4++)
        {
            afp k = lfp2afp(tmp_k[ly][z4]);
            afpvec4 v = afpvec4(
                lfp2afp(tmp_v[lx][z4][0]),
                lfp2afp(tmp_v[lx][z4][1]),
                lfp2afp(tmp_v[lx][z4][2]),
                lfp2afp(tmp_v[lx][z4][3]));
            sum += v * k;
        }

        v_offset += LOCAL_MEMORY_UNROLL_INCH * psc(cstep);
        w_offset += LOCAL_MEMORY_UNROLL_INCH;

        barrier();
    }

    if (z < c)
    {
        const int remain = c - z;

        if (ly < 4)
        {
            for (int r = 0; r < remain; r++)
            {
                tmp_v[lx][r][ly] = buffer_sm1(bottom_tm_blob_data, v_offset + r * psc(cstep) + ly);
            }
        }

        if (lx == 0)
        {
            for (int r = 0; r < remain; r++)
            {
                tmp_k[ly][r] = buffer_sm1(weight_tm_data, w_offset + r);
            }
        }

        barrier();

        int r = 0;
        for (; r + 3 < remain; r += 4)
        {
            afp k0 = lfp2afp(tmp_k[ly][r + 0]);
            afp k1 = lfp2afp(tmp_k[ly][r + 1]);
            afp k2 = lfp2afp(tmp_k[ly][r + 2]);
            afp k3 = lfp2afp(tmp_k[ly][r + 3]);

            afpvec4 v0 = afpvec4(
                lfp2afp(tmp_v[lx][r + 0][0]),
                lfp2afp(tmp_v[lx][r + 0][1]),
                lfp2afp(tmp_v[lx][r + 0][2]),
                lfp2afp(tmp_v[lx][r + 0][3]));
            afpvec4 v1 = afpvec4(
                lfp2afp(tmp_v[lx][r + 1][0]),
                lfp2afp(tmp_v[lx][r + 1][1]),
                lfp2afp(tmp_v[lx][r + 1][2]),
                lfp2afp(tmp_v[lx][r + 1][3]));
            afpvec4 v2 = afpvec4(
                lfp2afp(tmp_v[lx][r + 2][0]),
                lfp2afp(tmp_v[lx][r + 2][1]),
                lfp2afp(tmp_v[lx][r + 2][2]),
                lfp2afp(tmp_v[lx][r + 2][3]));
            afpvec4 v3 = afpvec4(
                lfp2afp(tmp_v[lx][r + 3][0]),
                lfp2afp(tmp_v[lx][r + 3][1]),
                lfp2afp(tmp_v[lx][r + 3][2]),
                lfp2afp(tmp_v[lx][r + 3][3]));

            sum += v0 * k0;
            sum += v1 * k1;
            sum += v2 * k2;
            sum += v3 * k3;
        }

        for (; r < remain; r++)
        {
            afp k = lfp2afp(tmp_k[ly][r]);
            afpvec4 v = afpvec4(
                lfp2afp(tmp_v[lx][r][0]),
                lfp2afp(tmp_v[lx][r][1]),
                lfp2afp(tmp_v[lx][r][2]),
                lfp2afp(tmp_v[lx][r][3]));
            sum += v * k;
        }
    }
#else
    int z = 0;
    for (; z + 3 < c; z += 4)
    {
        int v0 = v_offset + 0 * psc(cstep);
        int v1 = v_offset + 1 * psc(cstep);
        int v2 = v_offset + 2 * psc(cstep);
        int v3 = v_offset + 3 * psc(cstep);

        afp k0 = buffer_ld1(weight_tm_data, w_offset + 0);
        afp k1 = buffer_ld1(weight_tm_data, w_offset + 1);
        afp k2 = buffer_ld1(weight_tm_data, w_offset + 2);
        afp k3 = buffer_ld1(weight_tm_data, w_offset + 3);

        sum += afpvec4(
                   buffer_ld1(bottom_tm_blob_data, v0 + 0),
                   buffer_ld1(bottom_tm_blob_data, v0 + 1),
                   buffer_ld1(bottom_tm_blob_data, v0 + 2),
                   buffer_ld1(bottom_tm_blob_data, v0 + 3))
               * k0;

        sum += afpvec4(
                   buffer_ld1(bottom_tm_blob_data, v1 + 0),
                   buffer_ld1(bottom_tm_blob_data, v1 + 1),
                   buffer_ld1(bottom_tm_blob_data, v1 + 2),
                   buffer_ld1(bottom_tm_blob_data, v1 + 3))
               * k1;

        sum += afpvec4(
                   buffer_ld1(bottom_tm_blob_data, v2 + 0),
                   buffer_ld1(bottom_tm_blob_data, v2 + 1),
                   buffer_ld1(bottom_tm_blob_data, v2 + 2),
                   buffer_ld1(bottom_tm_blob_data, v2 + 3))
               * k2;

        sum += afpvec4(
                   buffer_ld1(bottom_tm_blob_data, v3 + 0),
                   buffer_ld1(bottom_tm_blob_data, v3 + 1),
                   buffer_ld1(bottom_tm_blob_data, v3 + 2),
                   buffer_ld1(bottom_tm_blob_data, v3 + 3))
               * k3;

        v_offset += 4 * psc(cstep);
        w_offset += 4;
    }

    for (; z < c; z++)
    {
        afpvec4 v = afpvec4(
            buffer_ld1(bottom_tm_blob_data, v_offset + 0),
            buffer_ld1(bottom_tm_blob_data, v_offset + 1),
            buffer_ld1(bottom_tm_blob_data, v_offset + 2),
            buffer_ld1(bottom_tm_blob_data, v_offset + 3));

        afp k = buffer_ld1(weight_tm_data, w_offset);

        sum += v * k;

        v_offset += psc(cstep);
        w_offset += 1;
    }
#endif

#if NCNN_shader_local_memory
    if (gx >= psc(outw) || gy >= outc || gz >= batch)
        return;
#endif

    int gi = gz * outc * psc(outcstep) + gy * psc(outcstep) + gx;

    buffer_st1(top_tm_blob_data, gi + 0, sum.x);
    buffer_st1(top_tm_blob_data, gi + 1, sum.y);
    buffer_st1(top_tm_blob_data, gi + 2, sum.z);
    buffer_st1(top_tm_blob_data, gi + 3, sum.w);
}
