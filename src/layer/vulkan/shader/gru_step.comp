// Copyright 2026 Futz12 <pchar.cn>
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout(binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout(binding = 1) readonly buffer weight_xc_blob { sfp weight_xc_data[]; };
layout(binding = 2) readonly buffer bias_c_blob { sfp bias_c_data[]; };
layout(binding = 3) readonly buffer weight_hc_blob { sfp weight_hc_data[]; };
layout(binding = 4) readonly buffer hidden_prev_blob { sfp hidden_prev_data[]; };
layout(binding = 5) writeonly buffer hidden_next_blob { sfp hidden_next_data[]; };
layout(binding = 6) writeonly buffer top_blob { sfp top_blob_data[]; };

layout(push_constant) uniform parameter
{
    int size;
    int num_output;
    int ti;
    int outw;
    int out_offset;
    int dir;
    int wxc_dir_stride;
    int whc_dir_stride;
    int bias_dir_stride;
    int bottom_step;
} p;

void main()
{
    int q = int(gl_GlobalInvocationID.x);

    if (q >= p.num_output)
        return;

    int x_offset = p.ti * p.bottom_step;

    int wxc_base = p.dir * p.wxc_dir_stride;
    int whc_base = p.dir * p.whc_dir_stride;
    int bias_base = p.dir * p.bias_dir_stride;

    int bias_R = bias_base + 0 * p.num_output + q;
    int bias_U = bias_base + 1 * p.num_output + q;
    int bias_WN = bias_base + 2 * p.num_output + q;
    int bias_BN = bias_base + 3 * p.num_output + q;

    int wxc_R = wxc_base + (0 * p.num_output + q) * p.size;
    int wxc_U = wxc_base + (1 * p.num_output + q) * p.size;
    int wxc_N = wxc_base + (2 * p.num_output + q) * p.size;

    int whc_R = whc_base + (0 * p.num_output + q) * p.num_output;
    int whc_U = whc_base + (1 * p.num_output + q) * p.num_output;
    int whc_N = whc_base + (2 * p.num_output + q) * p.num_output;

    afp R = buffer_ld1(bias_c_data, bias_R);
    afp U = buffer_ld1(bias_c_data, bias_U);

    for (int i = 0; i < p.size; i++)
    {
        afp xi = buffer_ld1(bottom_blob_data, x_offset + i);

        R += buffer_ld1(weight_xc_data, wxc_R + i) * xi;
        U += buffer_ld1(weight_xc_data, wxc_U + i) * xi;
    }

    for (int i = 0; i < p.num_output; i++)
    {
        afp hi = buffer_ld1(hidden_prev_data, i);

        R += buffer_ld1(weight_hc_data, whc_R + i) * hi;
        U += buffer_ld1(weight_hc_data, whc_U + i) * hi;
    }

    R = 1.f / (1.f + exp(-R));
    U = 1.f / (1.f + exp(-U));

    afp N = buffer_ld1(bias_c_data, bias_BN);
    for (int i = 0; i < p.num_output; i++)
    {
        afp hi = buffer_ld1(hidden_prev_data, i);
        N += buffer_ld1(weight_hc_data, whc_N + i) * hi;
    }

    N = buffer_ld1(bias_c_data, bias_WN) + R * N;

    for (int i = 0; i < p.size; i++)
    {
        afp xi = buffer_ld1(bottom_blob_data, x_offset + i);
        N += buffer_ld1(weight_xc_data, wxc_N + i) * xi;
    }

    N = tanh(N);

    afp hprev_q = buffer_ld1(hidden_prev_data, q);
    afp H = (1.f - U) * N + U * hprev_q;

    buffer_st1(hidden_next_data, q, H);

    int out_index = p.ti * p.outw + p.out_offset + q;
    buffer_st1(top_blob_data, out_index, H);
}