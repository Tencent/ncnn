// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2020 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
struct sfpvec8 { f16vec4 abcd; f16vec4 efgh; };
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int stride = 0;
layout (constant_id = 1) const int mode = 0;

#define shape_constant_id_offset 2
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler3D bottom_blob;
layout (binding = 1, imfmtc4) writeonly uniform unfp image3D top_blob;
#else
#if NCNN_fp16_packed
layout (binding = 0) readonly buffer bottom_blob { sfpvec2 bottom_blob_data[]; };
#else
layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
#endif
layout (binding = 1) writeonly buffer top_blob { sfpvec8 top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    ivec4 gz4 = gz * 8 + ivec4(0, 1, 2, 3);
    ivec4 gzz4 = gz4 + 4;

    ivec4 z4;
    ivec4 zi4;
    ivec4 zz4;
    ivec4 zii4;
    if (mode == 0)
    {
        z4 = gz4 / (stride * stride);
        zi4 = gz4 % (stride * stride);
        zz4 = gzz4 / (stride * stride);
        zii4 = gzz4 % (stride * stride);
    }
    else // if (mode == 1)
    {
        z4 = gz4 % (psc(c) * 8);
        zi4 = gz4 / (psc(c) * 8);
        zz4 = gzz4 % (psc(c) * 8);
        zii4 = gzz4 / (psc(c) * 8);
    }
    ivec4 y4 = gy * stride + zi4 / stride;
    ivec4 x4 = gx * stride + zi4 % stride;
    ivec4 yy4 = gy * stride + zii4 / stride;
    ivec4 xx4 = gx * stride + zii4 % stride;

#if NCNN_image_shader
    afpvec8 v0 = image3d_ld8(bottom_blob, ivec3(x4.r, y4.r, z4.r / 8));
    afpvec8 v1 = image3d_ld8(bottom_blob, ivec3(x4.g, y4.g, z4.g / 8));
    afpvec8 v2 = image3d_ld8(bottom_blob, ivec3(x4.b, y4.b, z4.b / 8));
    afpvec8 v3 = image3d_ld8(bottom_blob, ivec3(x4.a, y4.a, z4.a / 8));
    afpvec8 v4 = image3d_ld8(bottom_blob, ivec3(xx4.r, yy4.r, zz4.r / 8));
    afpvec8 v5 = image3d_ld8(bottom_blob, ivec3(xx4.g, yy4.g, zz4.g / 8));
    afpvec8 v6 = image3d_ld8(bottom_blob, ivec3(xx4.b, yy4.b, zz4.b / 8));
    afpvec8 v7 = image3d_ld8(bottom_blob, ivec3(xx4.a, yy4.a, zz4.a / 8));

    afpvec8 v;
    v[0].r = v0[(z4.r % 8) / 4][z4.r % 4];
    v[0].g = v1[(z4.g % 8) / 4][z4.g % 4];
    v[0].b = v2[(z4.b % 8) / 4][z4.b % 4];
    v[0].a = v3[(z4.a % 8) / 4][z4.a % 4];
    v[1].r = v4[(zz4.r % 8) / 4][zz4.r % 4];
    v[1].g = v5[(zz4.g % 8) / 4][zz4.g % 4];
    v[1].b = v6[(zz4.b % 8) / 4][zz4.b % 4];
    v[1].a = v7[(zz4.a % 8) / 4][zz4.a % 4];

    image3d_st8(top_blob, ivec3(gx, gy, gz), v);
#else
    int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

#if NCNN_fp16_packed
    ivec4 v_offset = ((z4 / 8) * psc(cstep) + y4 * psc(w) + x4) * 4 + (z4 % 8) / 2;
    ivec4 lane2 = z4 % 2;
    ivec4 vv_offset = ((zz4 / 8) * psc(cstep) + yy4 * psc(w) + xx4) * 4 + (zz4 % 8) / 2;
    ivec4 lane4 = zz4 % 2;

    afpvec2 vr = buffer_ld2(bottom_blob_data, v_offset.r);
    afpvec2 vg = buffer_ld2(bottom_blob_data, v_offset.g);
    afpvec2 vb = buffer_ld2(bottom_blob_data, v_offset.b);
    afpvec2 va = buffer_ld2(bottom_blob_data, v_offset.a);

    afpvec2 vvr = buffer_ld2(bottom_blob_data, vv_offset.r);
    afpvec2 vvg = buffer_ld2(bottom_blob_data, vv_offset.g);
    afpvec2 vvb = buffer_ld2(bottom_blob_data, vv_offset.b);
    afpvec2 vva = buffer_ld2(bottom_blob_data, vv_offset.a);

    afpvec8 v = afpvec8(vr[lane2.r], vg[lane2.g], vb[lane2.b], va[lane2.a], vvr[lane4.r], vvg[lane4.g], vvb[lane4.b], vva[lane4.a]);

    buffer_st8(top_blob_data, gi, v);
#else
    ivec4 v_offset = ((z4 / 8) * psc(cstep) + y4 * psc(w) + x4) * 8 + z4 % 8;
    ivec4 vv_offset = ((zz4 / 8) * psc(cstep) + yy4 * psc(w) + xx4) * 8 + zz4 % 8;

    buffer_cp1to8(top_blob_data, gi, bottom_blob_data, v_offset, vv_offset);
#endif
#endif
}
