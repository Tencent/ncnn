// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2020 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
struct sfpvec8 { f16vec4 abcd; f16vec4 efgh; };
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

#define shape_constant_id_offset 0
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler2D bottom_blob_2d;
layout (binding = 0) uniform unfp sampler3D bottom_blob_3d;
layout (binding = 1, imfmtc4) writeonly uniform unfp image1D top_blob;
#else
#if NCNN_fp16_packed
layout (binding = 0) readonly buffer bottom_blob { sfpvec2 bottom_blob_data[]; };
#else
layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
#endif
layout (binding = 1) writeonly buffer top_blob { sfpvec8 top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= 1 || gz >= 1)
        return;

    ivec4 i4 = gx * 8 + ivec4(0, 1, 2, 3);
    ivec4 ii4 = i4 + 4;

#if NCNN_image_shader
    afpvec8 v;

    if (psc(dims) == 2)
    {
        ivec4 y4 = i4 / psc(w);
        ivec4 x4 = i4 % psc(w);
        ivec4 yy4 = ii4 / psc(w);
        ivec4 xx4 = ii4 % psc(w);

        afpvec4 v0 = image2d_ld4(bottom_blob_2d, ivec2(x4.r, y4.r / 4));
        afpvec4 v1 = image2d_ld4(bottom_blob_2d, ivec2(x4.g, y4.g / 4));
        afpvec4 v2 = image2d_ld4(bottom_blob_2d, ivec2(x4.b, y4.b / 4));
        afpvec4 v3 = image2d_ld4(bottom_blob_2d, ivec2(x4.a, y4.a / 4));
        afpvec4 v4 = image2d_ld4(bottom_blob_2d, ivec2(xx4.r, yy4.r / 4));
        afpvec4 v5 = image2d_ld4(bottom_blob_2d, ivec2(xx4.g, yy4.g / 4));
        afpvec4 v6 = image2d_ld4(bottom_blob_2d, ivec2(xx4.b, yy4.b / 4));
        afpvec4 v7 = image2d_ld4(bottom_blob_2d, ivec2(xx4.a, yy4.a / 4));

        v[0].r = v0[y4.r % 4];
        v[0].g = v1[y4.g % 4];
        v[0].b = v2[y4.b % 4];
        v[0].a = v3[y4.a % 4];
        v[1].r = v4[yy4.r % 4];
        v[1].g = v5[yy4.g % 4];
        v[1].b = v6[yy4.b % 4];
        v[1].a = v7[yy4.a % 4];
    }
    else // if (psc(dims) == 3)
    {
        int size = psc(w) * psc(h);

        ivec4 z4 = i4 / size;
        ivec4 y4 = i4 % size / psc(w);
        ivec4 x4 = i4 % size % psc(w);
        ivec4 zz4 = ii4 / size;
        ivec4 yy4 = ii4 % size / psc(w);
        ivec4 xx4 = ii4 % size % psc(w);

        afpvec4 v0 = image3d_ld4(bottom_blob_3d, ivec3(x4.r, y4.r, z4.r / 4));
        afpvec4 v1 = image3d_ld4(bottom_blob_3d, ivec3(x4.g, y4.g, z4.g / 4));
        afpvec4 v2 = image3d_ld4(bottom_blob_3d, ivec3(x4.b, y4.b, z4.b / 4));
        afpvec4 v3 = image3d_ld4(bottom_blob_3d, ivec3(x4.a, y4.a, z4.a / 4));
        afpvec4 v4 = image3d_ld4(bottom_blob_3d, ivec3(xx4.r, yy4.r, zz4.r / 4));
        afpvec4 v5 = image3d_ld4(bottom_blob_3d, ivec3(xx4.g, yy4.g, zz4.g / 4));
        afpvec4 v6 = image3d_ld4(bottom_blob_3d, ivec3(xx4.b, yy4.b, zz4.b / 4));
        afpvec4 v7 = image3d_ld4(bottom_blob_3d, ivec3(xx4.a, yy4.a, zz4.a / 4));

        v[0].r = v0[z4.r % 4];
        v[0].g = v1[z4.g % 4];
        v[0].b = v2[z4.b % 4];
        v[0].a = v3[z4.a % 4];
        v[1].r = v4[zz4.r % 4];
        v[1].g = v5[zz4.g % 4];
        v[1].b = v6[zz4.b % 4];
        v[1].a = v7[zz4.a % 4];
    }

    image1d_st8(top_blob, gx, v);
#else
#if NCNN_fp16_packed
    ivec4 v_offset;
    ivec4 lane4;
    ivec4 vv_offset;
    ivec4 lane8;

    if (psc(dims) == 2)
    {
        ivec4 y4 = i4 / psc(w);
        ivec4 x4 = i4 % psc(w);
        ivec4 yy4 = ii4 / psc(w);
        ivec4 xx4 = ii4 % psc(w);

        v_offset = ((y4 / 4) * psc(w) + x4) * 2 + (y4 % 4) / 2;
        lane4 = y4 % 2;
        vv_offset = ((yy4 / 4) * psc(w) + xx4) * 2 + (yy4 % 4) / 2;
        lane8 = yy4 % 2;
    }
    else // if (psc(dims) == 3)
    {
        int size = psc(w) * psc(h);

        ivec4 z4 = i4 / size;
        ivec4 y4 = i4 % size / psc(w);
        ivec4 x4 = i4 % size % psc(w);
        ivec4 zz4 = ii4 / size;
        ivec4 yy4 = ii4 % size / psc(w);
        ivec4 xx4 = ii4 % size % psc(w);

        v_offset = ((z4 / 4) * psc(cstep) + y4 * psc(w) + x4) * 2 + (z4 % 4) / 2;
        lane4 = z4 % 2;
        vv_offset = ((zz4 / 4) * psc(cstep) + yy4 * psc(w) + xx4) * 2 + (zz4 % 4) / 2;
        lane8 = zz4 % 2;
    }

    afpvec2 vr = buffer_ld2(bottom_blob_data, v_offset.r);
    afpvec2 vg = buffer_ld2(bottom_blob_data, v_offset.g);
    afpvec2 vb = buffer_ld2(bottom_blob_data, v_offset.b);
    afpvec2 va = buffer_ld2(bottom_blob_data, v_offset.a);

    afpvec2 vvr = buffer_ld2(bottom_blob_data, vv_offset.r);
    afpvec2 vvg = buffer_ld2(bottom_blob_data, vv_offset.g);
    afpvec2 vvb = buffer_ld2(bottom_blob_data, vv_offset.b);
    afpvec2 vva = buffer_ld2(bottom_blob_data, vv_offset.a);

    afpvec8 v = afpvec8(vr[lane4.r], vg[lane4.g], vb[lane4.b], va[lane4.a], vvr[lane8.r], vvg[lane8.g], vvb[lane8.b], vva[lane8.a]);

    buffer_st8(top_blob_data, gx, v);
#else
    ivec4 v_offset;
    ivec4 vv_offset;

    if (psc(dims) == 2)
    {
        ivec4 y4 = i4 / psc(w);
        ivec4 x4 = i4 % psc(w);
        ivec4 yy4 = ii4 / psc(w);
        ivec4 xx4 = ii4 % psc(w);

        v_offset = ((y4 / 4) * psc(w) + x4) * 4 + y4 % 4;
        vv_offset = ((yy4 / 4) * psc(w) + xx4) * 4 + yy4 % 4;
    }
    else // if (psc(dims) == 3)
    {
        int size = psc(w) * psc(h);

        ivec4 z4 = i4 / size;
        ivec4 y4 = i4 % size / psc(w);
        ivec4 x4 = i4 % size % psc(w);
        ivec4 zz4 = ii4 / size;
        ivec4 yy4 = ii4 % size / psc(w);
        ivec4 xx4 = ii4 % size % psc(w);

        v_offset = ((z4 / 4) * psc(cstep) + y4 * psc(w) + x4) * 4 + z4 % 4;
        vv_offset = ((zz4 / 4) * psc(cstep) + yy4 * psc(w) + xx4) * 4 + zz4 % 4;
    }

    buffer_cp1to8(top_blob_data, gx, bottom_blob_data, v_offset, vv_offset);
#endif
#endif
}
