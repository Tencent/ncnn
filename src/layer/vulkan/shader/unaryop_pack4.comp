// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int op_type = 0;

#define shape_constant_id_offset 1
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler3D bottom_blob_3d;
layout (binding = 1, imfmtc4) writeonly uniform unfp image3D top_blob_3d;
#else
layout (binding = 0) buffer bottom_top_blob { sfpvec4 bottom_top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;
} p;

afpvec4 erf(afpvec4 x)
{
    afpvec4 a1 = afpvec4(0.254829592f);
    afpvec4 a2 = afpvec4(-0.284496736f);
    afpvec4 a3 = afpvec4(1.421413741f);
    afpvec4 a4 = afpvec4(-1.453152027f);
    afpvec4 a5 = afpvec4(1.061405429f);
    afpvec4 p = afpvec4(0.3275911f);
    afpvec4 s = sign(x);
    afpvec4 x_abs = abs(x);
    afpvec4 t = 1.0f / (1.0f + p * x_abs);
    afpvec4 y = 1.0f - (((((a5 * t + a4) * t) + a3) * t + a2) * t + a1) * t * exp(-x_abs * x_abs);
    return s * y;
}

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(w) || gy >= psc(h) || gz >= psc(c))
        return;

#if NCNN_image_shader
    afpvec4 v = image3d_ld4(bottom_blob_3d, ivec3(gx, gy, gz));
#else
    const int gi = gz * psc(cstep) + gy * psc(w) + gx;

    afpvec4 v = buffer_ld4(bottom_top_blob_data, gi);
#endif

    afpvec4 res;

    if (op_type == 0) res = abs(v);
    if (op_type == 1) res = -v;
    if (op_type == 2) res = floor(v);
    if (op_type == 3) res = ceil(v);
    if (op_type == 4) res = v * v;
    if (op_type == 5) res = sqrt(v);
    if (op_type == 6) res = inversesqrt(v);
    if (op_type == 7) res = exp(v);
    if (op_type == 8) res = log(v);
    if (op_type == 9) res = sin(v);
    if (op_type == 10) res = cos(v);
    if (op_type == 11) res = tan(v);
    if (op_type == 12) res = asin(v);
    if (op_type == 13) res = acos(v);
    if (op_type == 14) res = atan(v);
    if (op_type == 15) res = afp(1.f) / v;
#if NCNN_moltenvk
    if (op_type == 16) res = afpvec4(tanh(vec4(v)));
#else
    if (op_type == 16) res = tanh(v);
#endif
    if (op_type == 17) res = log(v) * afp(0.434294481903);
    if (op_type == 18) res = round(v);
    if (op_type == 19) res = trunc(v);
    if (op_type == 20) res = erf(v);

#if NCNN_image_shader
    image3d_st4(top_blob_3d, ivec3(gx, gy, gz), res);
#else
    buffer_st4(bottom_top_blob_data, gi, res);
#endif
}
