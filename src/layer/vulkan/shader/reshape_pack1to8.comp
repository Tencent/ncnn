// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2020 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
struct sfpvec8 { f16vec4 abcd; f16vec4 efgh; };
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int ndim = 0;

#define shape_constant_id_offset 1
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler1D bottom_blob_1d;
layout (binding = 0) uniform unfp sampler2D bottom_blob_2d;
layout (binding = 0) uniform unfp sampler3D bottom_blob_3d;
layout (binding = 1, imfmtc4) writeonly uniform unfp image1D top_blob_1d;
layout (binding = 1, imfmtc4) writeonly uniform unfp image2D top_blob_2d;
layout (binding = 1, imfmtc4) writeonly uniform unfp image3D top_blob_3d;
#else
layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfpvec8 top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    ivec4 i4;
    ivec4 ii4;

    if (ndim == 1)
    {
        i4 = gx * 8 + ivec4(0, 1, 2, 3);
        ii4 = i4 + 4;
    }
    if (ndim == 2)
    {
        i4 = (gy * 8) * psc(outw) + gx + ivec4(0, 1, 2, 3) * psc(outw);
        ii4 = i4 + 4 * psc(outw);
    }
    if (ndim == 3)
    {
        i4 = (gz * 8) * psc(outh) * psc(outw) + gy * psc(outw) + gx + ivec4(0, 1, 2, 3) * psc(outh) * psc(outw);
        ii4 = i4 + 4 * psc(outh) * psc(outw);
    }

    int size = psc(w) * psc(h);

    ivec4 z4 = i4 / size;
    ivec4 y4 = i4 % size / psc(w);
    ivec4 x4 = i4 % size % psc(w);
    ivec4 zz4 = ii4 / size;
    ivec4 yy4 = ii4 % size / psc(w);
    ivec4 xx4 = ii4 % size % psc(w);

#if NCNN_image_shader
    afpvec8 v;

    if (psc(dims) == 1)
    {
        v[0].r = image1d_ld1(bottom_blob_1d, x4.r);
        v[0].g = image1d_ld1(bottom_blob_1d, x4.g);
        v[0].b = image1d_ld1(bottom_blob_1d, x4.b);
        v[0].a = image1d_ld1(bottom_blob_1d, x4.a);
        v[1].r = image1d_ld1(bottom_blob_1d, xx4.r);
        v[1].g = image1d_ld1(bottom_blob_1d, xx4.g);
        v[1].b = image1d_ld1(bottom_blob_1d, xx4.b);
        v[1].a = image1d_ld1(bottom_blob_1d, xx4.a);
    }
    else if (psc(dims) == 2)
    {
        v[0].r = image2d_ld1(bottom_blob_2d, ivec2(x4.r, y4.r));
        v[0].g = image2d_ld1(bottom_blob_2d, ivec2(x4.g, y4.g));
        v[0].b = image2d_ld1(bottom_blob_2d, ivec2(x4.b, y4.b));
        v[0].a = image2d_ld1(bottom_blob_2d, ivec2(x4.a, y4.a));
        v[1].r = image2d_ld1(bottom_blob_2d, ivec2(xx4.r, yy4.r));
        v[1].g = image2d_ld1(bottom_blob_2d, ivec2(xx4.g, yy4.g));
        v[1].b = image2d_ld1(bottom_blob_2d, ivec2(xx4.b, yy4.b));
        v[1].a = image2d_ld1(bottom_blob_2d, ivec2(xx4.a, yy4.a));
    }
    else // if (psc(dims) == 3)
    {
        v[0].r = image3d_ld1(bottom_blob_3d, ivec3(x4.r, y4.r, z4.r));
        v[0].g = image3d_ld1(bottom_blob_3d, ivec3(x4.g, y4.g, z4.g));
        v[0].b = image3d_ld1(bottom_blob_3d, ivec3(x4.b, y4.b, z4.b));
        v[0].a = image3d_ld1(bottom_blob_3d, ivec3(x4.a, y4.a, z4.a));
        v[1].r = image3d_ld1(bottom_blob_3d, ivec3(xx4.r, yy4.r, zz4.r));
        v[1].g = image3d_ld1(bottom_blob_3d, ivec3(xx4.g, yy4.g, zz4.g));
        v[1].b = image3d_ld1(bottom_blob_3d, ivec3(xx4.b, yy4.b, zz4.b));
        v[1].a = image3d_ld1(bottom_blob_3d, ivec3(xx4.a, yy4.a, zz4.a));
    }

    if (ndim == 1)
    {
        image1d_st8(top_blob_1d, gx, v);
    }
    if (ndim == 2)
    {
        image2d_st8(top_blob_2d, ivec2(gx, gy), v);
    }
    if (ndim == 3)
    {
        image3d_st8(top_blob_3d, ivec3(gx, gy, gz), v);
    }
#else
    ivec4 v_offset = z4 * psc(cstep) + y4 * psc(w) + x4;
    ivec4 vv_offset = zz4 * psc(cstep) + yy4 * psc(w) + xx4;

    int gi;
    if (ndim == 1)
    {
        gi = gx;
    }
    if (ndim == 2)
    {
        gi = gy * psc(outw) + gx;
    }
    if (ndim == 3)
    {
        gi = gz * psc(outcstep) + gy * psc(outw) + gx;
    }

    buffer_cp1to8(top_blob_data, gi, bottom_blob_data, v_offset, vv_offset);
#endif
}
