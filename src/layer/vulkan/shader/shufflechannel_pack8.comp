// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2020 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
struct sfpvec8 { f16vec4 abcd; f16vec4 efgh; };
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int group = 1;

#define shape_constant_id_offset 1
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

layout (binding = 0) readonly buffer bottom_blob { sfpvec8 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfpvec8 top_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    const int channels_g = psc(c) * 8 / group;

    ivec4 gz4 = ivec4(gz * 8) + ivec4(0, 1, 2, 3);
    ivec4 gzz4 = gz4 + 4;

    ivec4 z4 = (gz4 % group) * channels_g + (gz4 / group);
    ivec4 zz4 = (gzz4 % group) * channels_g + (gzz4 / group);

    ivec4 v_offset = (z4 / 8) * psc(cstep) + gy * psc(w) + gx;
    ivec4 vv_offset = (zz4 / 8) * psc(cstep) + gy * psc(w) + gx;

    ivec4 sz4 = (z4 % 8) / 4;
    ivec4 szz4 = (zz4 % 8) / 4;

    ivec4 lane4 = z4 % 4;
    ivec4 lane8 = zz4 % 4;

    // v = v4[lane]
    int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

    afpvec8 vr = buffer_ld8(bottom_blob_data, v_offset.r);
    afpvec8 vg = buffer_ld8(bottom_blob_data, v_offset.g);
    afpvec8 vb = buffer_ld8(bottom_blob_data, v_offset.b);
    afpvec8 va = buffer_ld8(bottom_blob_data, v_offset.a);

    afpvec8 vvr = buffer_ld8(bottom_blob_data, vv_offset.r);
    afpvec8 vvg = buffer_ld8(bottom_blob_data, vv_offset.g);
    afpvec8 vvb = buffer_ld8(bottom_blob_data, vv_offset.b);
    afpvec8 vva = buffer_ld8(bottom_blob_data, vv_offset.a);

    afpvec8 v = afpvec8(vr[sz4.r][lane4.r], vg[sz4.g][lane4.g], vb[sz4.b][lane4.b], va[sz4.a][lane4.a], vvr[szz4.r][lane8.r], vvg[szz4.g][lane8.g], vvb[szz4.b][lane8.b], vva[szz4.a][lane8.a]);

    buffer_st8(top_blob_data, gi, v);
}
