// Copyright 2026 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout(constant_id = 0) const int M = 0;
layout(constant_id = 1) const int N = 0;
layout(constant_id = 2) const int K = 0;
layout(constant_id = 3) const int B = 0;
layout(constant_id = 4) const int num_heads_per_group = 0;
layout(constant_id = 5) const int qk_cstep = 0;
layout(constant_id = 6) const int v_cstep = 0;
layout(constant_id = 7) const int out_cstep = 0;

layout(binding = 0) readonly buffer qkcross_blob { sfp qkcross_blob_data[]; };
layout(binding = 1) readonly buffer v_blob { sfp v_blob_data[]; };
layout(binding = 2) writeonly buffer qkvcross_blob { sfp qkvcross_blob_data[]; };

layout(push_constant) uniform parameter
{
    int M;
    int N;
    int K;
    int B;
    int num_heads_per_group;
    int qk_cstep;
    int v_cstep;
    int out_cstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(N) || gy >= psc(M) || gz >= psc(B))
        return;

    afp sum = afp(0.f);

    for (int k = 0; k < psc(K); k++)
    {
        const int ai = gz * psc(qk_cstep) + gy * psc(K) + k;
        afp qk0 = buffer_ld1(qkcross_blob_data, ai);

        const int bi = (gz / psc(num_heads_per_group)) * psc(v_cstep) + k * psc(N) + gx;
        afp v0 = buffer_ld1(v_blob_data, bi);

        sum += qk0 * v0;
    }

    const int gi = gz * psc(out_cstep) + gy * psc(N) + gx;

    buffer_st1(qkvcross_blob_data, gi, sum);
}
