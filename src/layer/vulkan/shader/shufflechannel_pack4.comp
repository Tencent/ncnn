// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int group = 1;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

layout (binding = 0) readonly buffer bottom_blob { sfpvec4 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfpvec4 top_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.outw || gy >= p.outh || gz >= p.outc)
        return;

    const int channels_g = p.c * 4 / group;

    ivec4 gz4 = ivec4(gz * 4) + ivec4(0, 1, 2, 3);

    ivec4 z4 = (gz4 % group) * channels_g + (gz4 / group);

    ivec4 v_offset = (z4 / 4) * p.cstep + gy * p.w + gx;

    ivec4 lane4 = z4 % 4;

    // v = v4[lane]
    int gi = gz * p.outcstep + gy * p.outw + gx;

#if NCNN_fp16_packed
    vec4 vr = sfp2afpvec4(bottom_blob_data[v_offset.r]);
    vec4 vg = sfp2afpvec4(bottom_blob_data[v_offset.g]);
    vec4 vb = sfp2afpvec4(bottom_blob_data[v_offset.b]);
    vec4 va = sfp2afpvec4(bottom_blob_data[v_offset.a]);

    vec2 v0 = vec2(vr[lane4.r], vg[lane4.g]);
    vec2 v1 = vec2(vb[lane4.b], va[lane4.a]);

    top_blob_data[gi] = uvec2(packHalf2x16(v0), packHalf2x16(v1));
#elif NCNN_fp16_storage && !NCNN_fp16_arithmetic
    top_blob_data[gi].r = afp2sfp(sfp2afpvec4(bottom_blob_data[v_offset.r])[lane4.r]);
    top_blob_data[gi].g = afp2sfp(sfp2afpvec4(bottom_blob_data[v_offset.g])[lane4.g]);
    top_blob_data[gi].b = afp2sfp(sfp2afpvec4(bottom_blob_data[v_offset.b])[lane4.b]);
    top_blob_data[gi].a = afp2sfp(sfp2afpvec4(bottom_blob_data[v_offset.a])[lane4.a]);
#else
    top_blob_data[gi].r = bottom_blob_data[v_offset.r][lane4.r];
    top_blob_data[gi].g = bottom_blob_data[v_offset.g][lane4.g];
    top_blob_data[gi].b = bottom_blob_data[v_offset.b][lane4.b];
    top_blob_data[gi].a = bottom_blob_data[v_offset.a][lane4.a];
#endif
}
