// Copyright 2020 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout (constant_id = 0) const int across_spatial = 0;
layout (constant_id = 1) const int across_channel = 0;
layout (constant_id = 2) const float eps = 0.f;
layout (constant_id = 3) const int eps_mode = 0;

layout (binding = 0) readonly buffer sqsum_blob { mat2x4 sqsum_blob_data[]; };
layout (binding = 1) writeonly buffer coeffs_blob { sfpvec8 coeffs_blob_data[]; };

layout (push_constant) uniform parameter
{
    int w;
    int h;
    int c;
    int cstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.w || gy >= p.h || gz >= p.c)
        return;

    int v_offset = gz * p.cstep + gx;

    mat2x4 sqsum_8 = sqsum_blob_data[v_offset];

    if (across_channel == 0)
    {
        afpvec8 sqsum = afpvec8(sqsum_8[0], sqsum_8[1]);

        afpvec8 a;

        if (eps_mode == 0) // caffe/mxnet
        {
            a[0] = afp(1.f) / sqrt(sqsum[0] + afp(eps));
            a[1] = afp(1.f) / sqrt(sqsum[1] + afp(eps));
        }

        if (eps_mode == 1) // pytorch
        {
            a[0] = afp(1.f) / max(sqrt(sqsum[0]), afp(eps));
            a[1] = afp(1.f) / max(sqrt(sqsum[1]), afp(eps));
        }

        if (eps_mode == 2) // tensorflow
        {
            a[0] = afp(1.f) / sqrt(max(sqsum[0], afp(eps)));
            a[1] = afp(1.f) / sqrt(max(sqsum[1], afp(eps)));
        }

        int gi = gz * p.w + gx;

        buffer_st8(coeffs_blob_data, gi, a);
    }

    if (across_channel == 1)
    {
        vec4 sqsum_4 = sqsum_8[0] + sqsum_8[1];

        afp sqsum = afp(sqsum_4.r + sqsum_4.g + sqsum_4.b + sqsum_4.a);

        afp a;

        if (eps_mode == 0) // caffe/mxnet
        {
            a = afp(1.f) / sqrt(sqsum + afp(eps));
        }

        if (eps_mode == 1) // pytorch
        {
            a = afp(1.f) / max(sqrt(sqsum), afp(eps));
        }

        if (eps_mode == 2) // tensorflow
        {
            a = afp(1.f) / sqrt(max(sqsum, afp(eps)));
        }

        afpvec8 a8 = afpvec8(afpvec4(a), afpvec4(a));

        int gi = gz * p.w + gx;

        buffer_st8(coeffs_blob_data, gi, a8);
    }
}
