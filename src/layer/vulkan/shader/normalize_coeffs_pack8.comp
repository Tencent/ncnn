// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2020 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
struct sfpvec8 { f16vec4 abcd; f16vec4 efgh; };
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int across_spatial = 0;
layout (constant_id = 1) const int across_channel = 0;
layout (constant_id = 2) const float eps = 0.f;
layout (constant_id = 3) const int eps_mode = 0;

#if NCNN_image_shader
layout (binding = 0) uniform highp sampler3D sqsum_blob;
layout (binding = 1, imfmtc4) writeonly uniform unfp image3D coeffs_blob;
#else
layout (binding = 0) readonly buffer sqsum_blob { mat2x4 sqsum_blob_data[]; };
layout (binding = 1) writeonly buffer coeffs_blob { sfpvec8 coeffs_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int w;
    int h;
    int c;
    int cstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.w || gy >= p.h || gz >= p.c)
        return;

#if NCNN_image_shader
    mat2x4 sqsum_8 = mat2x4(texelFetch(sqsum_blob, ivec3(gx * 2, gy, gz), 0), texelFetch(sqsum_blob, ivec3(gx * 2 + 1, gy, gz), 0));
#else
    int v_offset = gz * p.cstep + gx;

    mat2x4 sqsum_8 = sqsum_blob_data[v_offset];
#endif

    if (across_channel == 0)
    {
        afpvec8 sqsum = afpvec8(sqsum_8[0], sqsum_8[1]);

        afpvec8 a;

        if (eps_mode == 0) // caffe/mxnet
        {
            a[0] = afp(1.f) / sqrt(sqsum[0] + afp(eps));
            a[1] = afp(1.f) / sqrt(sqsum[1] + afp(eps));
        }

        if (eps_mode == 1) // pytorch
        {
            a[0] = afp(1.f) / max(sqrt(sqsum[0]), afp(eps));
            a[1] = afp(1.f) / max(sqrt(sqsum[1]), afp(eps));
        }

        if (eps_mode == 2) // tensorflow
        {
            a[0] = afp(1.f) / sqrt(max(sqsum[0], afp(eps)));
            a[1] = afp(1.f) / sqrt(max(sqsum[1], afp(eps)));
        }

#if NCNN_image_shader
        int gi = gz * p.w * p.h + gy * p.w + gx;

        image3d_st8(coeffs_blob, ivec3(gi, 0, 0), a);
#else
        int gi = gz * p.w + gx;

        buffer_st8(coeffs_blob_data, gi, a);
#endif
    }

    if (across_channel == 1)
    {
        vec4 sqsum_4 = sqsum_8[0] + sqsum_8[1];

        afp sqsum = afp(sqsum_4.r + sqsum_4.g + sqsum_4.b + sqsum_4.a);

        afp a;

        if (eps_mode == 0) // caffe/mxnet
        {
            a = afp(1.f) / sqrt(sqsum + afp(eps));
        }

        if (eps_mode == 1) // pytorch
        {
            a = afp(1.f) / max(sqrt(sqsum), afp(eps));
        }

        if (eps_mode == 2) // tensorflow
        {
            a = afp(1.f) / sqrt(max(sqsum, afp(eps)));
        }

        afpvec8 a8 = afpvec8(afpvec4(a), afpvec4(a));

#if NCNN_image_shader
        int gi = gz * p.w * p.h + gy * p.w + gx;

        image3d_st8(coeffs_blob, ivec3(gi, 0, 0), a8);
#else
        int gi = gz * p.w + gx;

        buffer_st8(coeffs_blob_data, gi, a8);
#endif
    }
}
