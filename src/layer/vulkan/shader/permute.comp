// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_AMD_gpu_shader_half_float: require
#endif

layout (constant_id = 0) const int order_type = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfp top_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.outw || gy >= p.outh || gz >= p.outc)
        return;

    int v_offset;

    if (p.dims == 2)
    {
        // order_type
        // 0 = w h
        // 1 = h w

        if (order_type == 0)
        {
            v_offset = gy * p.w + gx;
        }
        if (order_type == 1)
        {
            v_offset = gx * p.w + gy;
        }
    }
    else if (p.dims == 3)
    {
        // order_type
        // 0 = w h c
        // 1 = h w c
        // 2 = w c h
        // 3 = c w h
        // 4 = h c w
        // 5 = c h w

        if (order_type == 0)
        {
            v_offset = gz * p.cstep + gy * p.w + gx;
        }
        if (order_type == 1)
        {
            v_offset = gz * p.cstep + gx * p.w + gy;
        }
        if (order_type == 2)
        {
            v_offset = gy * p.cstep + gz * p.w + gx;
        }
        if (order_type == 3)
        {
            v_offset = gx * p.cstep + gz * p.w + gy;
        }
        if (order_type == 4)
        {
            v_offset = gy * p.cstep + gx * p.w + gz;
        }
        if (order_type == 5)
        {
            v_offset = gx * p.cstep + gy * p.w + gz;
        }
    }

    top_blob_data[gz * p.outcstep + gy * p.outw + gx] = bottom_blob_data[v_offset];
}
