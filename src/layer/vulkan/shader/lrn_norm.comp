// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int region_type = 0;
layout (constant_id = 1) const int local_size = 0;
layout (constant_id = 2) const float alpha = 0;
layout (constant_id = 3) const float beta = 0;
layout (constant_id = 4) const float bias_constant = 0;

#define shape_constant_id_offset 5
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

#if NCNN_image_shader
layout (binding = 0) uniform highp sampler3D square_workspace;
layout (binding = 1) uniform unfp sampler3D bottom_blob;
layout (binding = 2, imfmtc1) writeonly uniform unfp image3D top_blob;
#else
layout (binding = 0) readonly buffer square_workspace { float square_workspace_data[]; };
layout (binding = 1) buffer bottom_top_blob { sfp bottom_top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    afp scale;

    if (region_type == 0)
    {
        afp sum = afp(0.f);

#if NCNN_image_shader
        for (int z = 0; z < local_size; z++)
        {
            sum += afp(texelFetch(square_workspace, ivec3(gx, gy, gz + z), 0).r);
        }
#else
        int v_offset = gz * psc(cstep) + gy * psc(w) + gx;

        for (int z = 0; z < local_size; z++)
        {
            sum += afp(square_workspace_data[v_offset]);

            v_offset += psc(cstep);
        }
#endif

        const afp alpha_div_size = afp(alpha / local_size);
        scale = pow(afp(bias_constant) + alpha_div_size * sum, afp(-beta));
    }
    else
    {
        afp sum = afp(0.f);

#if NCNN_image_shader
        for (int y = 0; y < local_size; y++)
        {
            for (int x = 0; x < local_size; x++)
            {
                sum += afp(texelFetch(square_workspace, ivec3(gx + x, gy + y, gz), 0).r);
            }
        }
#else
        int v_offset = gz * psc(cstep) + gy * psc(w) + gx;

        for (int y = 0; y < local_size; y++)
        {
            for (int x = 0; x < local_size; x++)
            {
                sum += afp(square_workspace_data[v_offset + x]);
            }

            v_offset += psc(w);
        }
#endif

        const afp alpha_div_size = afp(alpha / (local_size * local_size));
        scale = pow(afp(bias_constant) + alpha_div_size * sum, afp(-beta));
    }

#if NCNN_image_shader
    afp v = image3d_ld1(bottom_blob, ivec3(gx, gy, gz));
#else
    int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

    afp v = buffer_ld1(bottom_top_blob_data, gi);
#endif

    v *= scale;

#if NCNN_image_shader
    image3d_st1(top_blob, ivec3(gx, gy, gz), v);
#else
    buffer_st1(bottom_top_blob_data, gi, v);
#endif
}
