// Copyright 2019 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout (constant_id = 0) const int axis = 0;

#define shape_constant_id_offset 1
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int d = 0;
layout (constant_id = shape_constant_id_offset + 4) const int c = 0;
layout (constant_id = shape_constant_id_offset + 5) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 6) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outd = 0;
layout (constant_id = shape_constant_id_offset + 10) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 11) const int outcstep = 0;

layout (binding = 0) readonly buffer bottom_blob { sfpvec4 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfpvec4 top_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int d;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outd;
    int outc;
    int outcstep;

    int offset;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) * psc(outd) || gz >= psc(outc))
        return;

    int positive_axis = axis < 0 ? psc(dims) + axis : axis;

    ivec3 gxyz;

    if (psc(dims) == 4)
    {
        int yd = gy / psc(outh);
        int yh = gy % psc(outh);

        ivec4 gxydz = ivec4(gx, yh, yd, gz);
        gxydz[psc(dims) - 1 - positive_axis] += p.offset;

        gxyz = ivec3(gxydz.r, gxydz.g + gxydz.b * psc(h), gxydz.a);
    }
    else
    {
        gxyz = ivec3(gx, gy, gz);
        gxyz[psc(dims) - 1 - positive_axis] += p.offset;
    }

    const int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

    int v_offset = gxyz.z * psc(cstep) + gxyz.y * psc(w) + gxyz.x;

    buffer_cp4(top_blob_data, gi, bottom_blob_data, v_offset);
}
