// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2020 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

layout (constant_id = 0) const int storage_type_from = 0;
layout (constant_id = 1) const int storage_type_to = 0;

#define shape_constant_id_offset 2
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob_fp32 { mat2x4 top_blob_fp32_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    afpvec8 v;

    if (psc(dims) == 1)
    {
        if (storage_type_from == 0)
        {
            ivec4 x4 = ivec4(gx * 8) + ivec4(0, 1, 2, 3);

            ivec4 v_offset = x4;
            ivec4 vv_offset = x4 + 4;

            v[0].r = buffer_ld1(bottom_blob_data, v_offset.r);
            v[0].g = buffer_ld1(bottom_blob_data, v_offset.g);
            v[0].b = buffer_ld1(bottom_blob_data, v_offset.b);
            v[0].a = buffer_ld1(bottom_blob_data, v_offset.a);
            v[1].r = buffer_ld1(bottom_blob_data, vv_offset.r);
            v[1].g = buffer_ld1(bottom_blob_data, vv_offset.g);
            v[1].b = buffer_ld1(bottom_blob_data, vv_offset.b);
            v[1].a = buffer_ld1(bottom_blob_data, vv_offset.a);
        }

        if (storage_type_to == 0)
        {
            int gi = gx;

            top_blob_fp32_data[gi] = mat2x4(vec4(v[0]), vec4(v[1]));
        }
    }
    else if (psc(dims) == 2)
    {
        if (storage_type_from == 0)
        {
            ivec4 y4 = ivec4(gy * 8) + ivec4(0, 1, 2, 3);

            ivec4 v_offset = y4 * psc(w) + gx;
            ivec4 vv_offset = (y4 + 4) * psc(w) + gx;

            v[0].r = buffer_ld1(bottom_blob_data, v_offset.r);
            v[0].g = buffer_ld1(bottom_blob_data, v_offset.g);
            v[0].b = buffer_ld1(bottom_blob_data, v_offset.b);
            v[0].a = buffer_ld1(bottom_blob_data, v_offset.a);
            v[1].r = buffer_ld1(bottom_blob_data, vv_offset.r);
            v[1].g = buffer_ld1(bottom_blob_data, vv_offset.g);
            v[1].b = buffer_ld1(bottom_blob_data, vv_offset.b);
            v[1].a = buffer_ld1(bottom_blob_data, vv_offset.a);
        }

        if (storage_type_to == 0)
        {
            int gi = gy * psc(outw) + gx;

            top_blob_fp32_data[gi] = mat2x4(vec4(v[0]), vec4(v[1]));
        }
    }
    else // if (psc(dims) == 3)
    {
        if (storage_type_from == 0)
        {
            ivec4 z4 = ivec4(gz * 8) + ivec4(0, 1, 2, 3);

            ivec4 v_offset = z4 * psc(cstep) + ivec4(gy * psc(w) + gx);
            ivec4 vv_offset = (z4 + 4) * psc(cstep) + ivec4(gy * psc(w) + gx);

            v[0].r = buffer_ld1(bottom_blob_data, v_offset.r);
            v[0].g = buffer_ld1(bottom_blob_data, v_offset.g);
            v[0].b = buffer_ld1(bottom_blob_data, v_offset.b);
            v[0].a = buffer_ld1(bottom_blob_data, v_offset.a);
            v[1].r = buffer_ld1(bottom_blob_data, vv_offset.r);
            v[1].g = buffer_ld1(bottom_blob_data, vv_offset.g);
            v[1].b = buffer_ld1(bottom_blob_data, vv_offset.b);
            v[1].a = buffer_ld1(bottom_blob_data, vv_offset.a);
        }

        if (storage_type_to == 0)
        {
            int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

            top_blob_fp32_data[gi] = mat2x4(vec4(v[0]), vec4(v[1]));
        }
    }
}
