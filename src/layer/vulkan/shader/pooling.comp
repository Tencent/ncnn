// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2018 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

#define FLT_MAX 3.402823466e+38

layout (constant_id = 0) const int pooling_type = 0;
layout (constant_id = 1) const int kernel_w = 1;
layout (constant_id = 2) const int kernel_h = 1;
layout (constant_id = 3) const int stride_w = 1;
layout (constant_id = 4) const int stride_h = 1;
layout (constant_id = 5) const int pad_left = 0;
layout (constant_id = 6) const int pad_right = 0;
layout (constant_id = 7) const int pad_top = 0;
layout (constant_id = 8) const int pad_bottom = 0;
layout (constant_id = 9) const int global_pooling = 0;
layout (constant_id = 10) const int pad_mode = 0;
layout (constant_id = 11) const int avgpool_count_include_pad = 0;

#define shape_constant_id_offset 12
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler3D bottom_blob;
layout (binding = 1, imfmtc1) writeonly uniform unfp image3D top_blob;
#else
layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfp top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;

    int wtailpad;
    int htailpad;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    afp res;

    if (pooling_type == 0)
    {
        res = afp(-FLT_MAX);

#if NCNN_image_shader
        int sx = gx * stride_w;
        int sy = gy * stride_h;

        for (int y = 0; y < kernel_h; y++)
        {
            for (int x = 0; x < kernel_w; x++)
            {
                afp v = image3d_ld1(bottom_blob, ivec3(sx + x, sy + y, gz));
                res = max(res, v);
            }
        }
#else
        int v_offset = gz * psc(cstep) + gy * stride_h * psc(w) + gx * stride_w;

        for (int y = 0; y < kernel_h; y++)
        {
            for (int x = 0; x < kernel_w; x++)
            {
                afp v = buffer_ld1(bottom_blob_data, v_offset + x);
                res = max(res, v);
            }

            v_offset += psc(w);
        }
#endif
    }
    if (pooling_type == 1 && avgpool_count_include_pad == 0)
    {
        res = afp(0.f);
        int area = 0;

        int sx = gx * stride_w;
        int sy = gy * stride_h;

#if NCNN_image_shader
        for (int y = 0; y < kernel_h; y++)
        {
            if (sy + y < pad_top)
                continue;

            if (sy + y >= psc(h) - pad_bottom - p.htailpad)
                break;

            for (int x = 0; x < kernel_w; x++)
            {
                if (sx + x < pad_left)
                    continue;

                if (sx + x >= psc(w) - pad_right - p.wtailpad)
                    break;

                res += image3d_ld1(bottom_blob, ivec3(sx + x, sy + y, gz));
                area += 1;
            }
        }
#else
        int v_offset = gz * psc(cstep) + sy * psc(w) + sx;

        for (int y = 0; y < kernel_h; y++)
        {
            if (sy + y < pad_top)
            {
                v_offset += psc(w);
                continue;
            }

            if (sy + y >= psc(h) - pad_bottom - p.htailpad)
                break;

            for (int x = 0; x < kernel_w; x++)
            {
                if (sx + x < pad_left)
                {
                    continue;
                }

                if (sx + x >= psc(w) - pad_right - p.wtailpad)
                    break;

                res += buffer_ld1(bottom_blob_data, v_offset + x);
                area += 1;
            }

            v_offset += psc(w);
        }
#endif

        res /= afp(area);
    }
    if (pooling_type == 1 && avgpool_count_include_pad == 1)
    {
        res = afp(0.f);

#if NCNN_image_shader
        int sx = gx * stride_w;
        int sy = gy * stride_h;

        for (int y = 0; y < kernel_h; y++)
        {
            for (int x = 0; x < kernel_w; x++)
            {
                res += image3d_ld1(bottom_blob, ivec3(sx + x, sy + y, gz));
            }
        }
#else
        int v_offset = gz * psc(cstep) + gy * stride_h * psc(w) + gx * stride_w;

        for (int y = 0; y < kernel_h; y++)
        {
            for (int x = 0; x < kernel_w; x++)
            {
                res += buffer_ld1(bottom_blob_data, v_offset + x);
            }

            v_offset += psc(w);
        }
#endif

        res /= afp(kernel_w * kernel_h);
    }

#if NCNN_image_shader
    image3d_st1(top_blob, ivec3(gx, gy, gz), res);
#else
    const int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

    buffer_st1(top_blob_data, gi, res);
#endif
}
