// Copyright 2020 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout (constant_id = 0) const int type = 1;
layout (constant_id = 1) const float value = 0;
layout (constant_id = 2) const int per_channel_pad = 0;

#define shape_constant_id_offset 3
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

#if NCNN_fp16_packed
layout (binding = 0) readonly buffer bottom_blob { sfpvec2 bottom_blob_data[]; };
#else
layout (binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
#endif
layout (binding = 1) writeonly buffer top_blob { sfp top_blob_data[]; };
layout (binding = 2) readonly buffer per_channel_pad_blob { sfp per_channel_pad_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;

    int left;
    int top;
    int front;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(outw) || gy >= psc(outh) || gz >= psc(outc))
        return;

    if (psc(dims) == 1)
    {
        int x = gx - p.left;

        if (type == 0)
        {
            if (x >= 0 && x < psc(w) * 4)
            {
#if NCNN_fp16_packed
                int v_offset = (x / 4) * 2 + (x % 4) / 2;
                int lane2 = x % 2;

                afpvec2 v = buffer_ld2(bottom_blob_data, v_offset);

                buffer_st1(top_blob_data, gx, v[lane2]);
#else
                int v_offset = (x / 4) * 4 + x % 4;

                buffer_cp1(top_blob_data, gx, bottom_blob_data, v_offset);
#endif
            }
            else
            {
                afp v = afp(value);
                buffer_st1(top_blob_data, gx, v);
            }
        }
        if (type == 1)
        {
            x = clamp(x, 0, psc(w) * 4 - 1);

#if NCNN_fp16_packed
            int v_offset = (x / 4) * 2 + (x % 4) / 2;
            int lane2 = x % 2;

            afpvec2 v = buffer_ld2(bottom_blob_data, v_offset);

            buffer_st1(top_blob_data, gx, v[lane2]);
#else
            int v_offset = (x / 4) * 4 + x % 4;

            buffer_cp1(top_blob_data, gx, bottom_blob_data, v_offset);
#endif
        }
        if (type == 2)
        {
            x = abs(x);
            // NOTE psc(X) get zeros on nvidia
            // TODO only enable this workaround for some nvidia driver
            x = (p.w * 4 - 1) - abs(x - (p.w * 4 - 1));
//             x = (psc(w) * 4 - 1) - abs(x - (psc(w) * 4 - 1));

#if NCNN_fp16_packed
            int v_offset = (x / 4) * 2 + (x % 4) / 2;
            int lane2 = x % 2;

            afpvec2 v = buffer_ld2(bottom_blob_data, v_offset);

            buffer_st1(top_blob_data, gx, v[lane2]);
#else
            int v_offset = (x / 4) * 4 + x % 4;

            buffer_cp1(top_blob_data, gx, bottom_blob_data, v_offset);
#endif
        }
    }
    else if (psc(dims) == 2)
    {
        const int gi = gy * psc(outw) + gx;

        int x = gx - p.left;
        int y = gy - p.top;

        if (type == 0)
        {
            if (x >= 0 && x < psc(w) && y >= 0 && y < psc(h) * 4)
            {
#if NCNN_fp16_packed
                int v_offset = ((y / 4) * psc(w) + x) * 2 + (y % 4) / 2;
                int lane2 = y % 2;

                afpvec2 v = buffer_ld2(bottom_blob_data, v_offset);

                buffer_st1(top_blob_data, gi, v[lane2]);
#else
                int v_offset = ((y / 4) * psc(w) + x) * 4 + y % 4;

                buffer_cp1(top_blob_data, gi, bottom_blob_data, v_offset);
#endif
            }
            else
            {
                afp v = afp(value);
                buffer_st1(top_blob_data, gi, v);
            }
        }
        if (type == 1)
        {
            x = clamp(x, 0, psc(w) - 1);
            y = clamp(y, 0, psc(h) * 4 - 1);

#if NCNN_fp16_packed
            int v_offset = ((y / 4) * psc(w) + x) * 2 + (y % 4) / 2;
            int lane2 = y % 2;

            afpvec2 v = buffer_ld2(bottom_blob_data, v_offset);

            buffer_st1(top_blob_data, gi, v[lane2]);
#else
            int v_offset = ((y / 4) * psc(w) + x) * 4 + y % 4;

            buffer_cp1(top_blob_data, gi, bottom_blob_data, v_offset);
#endif
        }
        if (type == 2)
        {
            x = abs(x);
            y = abs(y);
            // NOTE psc(X) get zeros on nvidia
            // TODO only enable this workaround for some nvidia driver
            x = (p.w - 1) - abs(x - (p.w - 1));
            y = (p.h * 4 - 1) - abs(y - (p.h * 4 - 1));
//             x = (psc(w) - 1) - abs(x - (psc(w) - 1));
//             y = (psc(h) * 4 - 1) - abs(y - (psc(h) * 4 - 1));

#if NCNN_fp16_packed
            int v_offset = ((y / 4) * psc(w) + x) * 2 + (y % 4) / 2;
            int lane2 = y % 2;

            afpvec2 v = buffer_ld2(bottom_blob_data, v_offset);

            buffer_st1(top_blob_data, gi, v[lane2]);
#else
            int v_offset = ((y / 4) * psc(w) + x) * 4 + y % 4;

            buffer_cp1(top_blob_data, gi, bottom_blob_data, v_offset);
#endif
        }
    }
    else // if (psc(dims) == 3)
    {
        const int gi = gz * psc(outcstep) + gy * psc(outw) + gx;

        int x = gx - p.left;
        int y = gy - p.top;
        int z = gz - p.front;

        if (type == 0)
        {
            if (x >= 0 && x < psc(w) && y >= 0 && y < psc(h) && z >= 0 && z < psc(c) * 4)
            {
#if NCNN_fp16_packed
                int v_offset = ((z / 4) * psc(cstep) + y * psc(w) + x) * 2 + (z % 4) / 2;
                int lane2 = z % 2;

                afpvec2 v = buffer_ld2(bottom_blob_data, v_offset);

                buffer_st1(top_blob_data, gi, v[lane2]);
#else
                int v_offset = ((z / 4) * psc(cstep) + y * psc(w) + x) * 4 + z % 4;

                buffer_cp1(top_blob_data, gi, bottom_blob_data, v_offset);
#endif
            }
            else if (per_channel_pad == 1)
            {
                buffer_cp1(top_blob_data, gi, per_channel_pad_blob_data, gz);
            }
            else
            {
                afp v = afp(value);
                buffer_st1(top_blob_data, gi, v);
            }
        }
        if (type == 1)
        {
            x = clamp(x, 0, psc(w) - 1);
            y = clamp(y, 0, psc(h) - 1);
            z = clamp(z, 0, psc(c) * 4 - 1);

#if NCNN_fp16_packed
            int v_offset = ((z / 4) * psc(cstep) + y * psc(w) + x) * 2 + (z % 4) / 2;
            int lane2 = z % 2;

            afpvec2 v = buffer_ld2(bottom_blob_data, v_offset);

            buffer_st1(top_blob_data, gi, v[lane2]);
#else
            int v_offset = ((z / 4) * psc(cstep) + y * psc(w) + x) * 4 + z % 4;

            buffer_cp1(top_blob_data, gi, bottom_blob_data, v_offset);
#endif
        }
        if (type == 2)
        {
            x = abs(x);
            y = abs(y);
            z = abs(z);
            // NOTE psc(X) get zeros on nvidia
            // TODO only enable this workaround for some nvidia driver
            x = (p.w - 1) - abs(x - (p.w - 1));
            y = (p.h - 1) - abs(y - (p.h - 1));
            z = (p.c * 4 - 1) - abs(z - (p.c * 4 - 1));
//             x = (psc(w) - 1) - abs(x - (psc(w) - 1));
//             y = (psc(h) - 1) - abs(y - (psc(h) - 1));
//             z = (psc(c) * 4 - 1) - abs(z - (psc(c) * 4 - 1));

#if NCNN_fp16_packed
            int v_offset = ((z / 4) * psc(cstep) + y * psc(w) + x) * 2 + (z % 4) / 2;
            int lane2 = z % 2;

            afpvec2 v = buffer_ld2(bottom_blob_data, v_offset);

            buffer_st1(top_blob_data, gi, v[lane2]);
#else
            int v_offset = ((z / 4) * psc(cstep) + y * psc(w) + x) * 4 + z % 4;

            buffer_cp1(top_blob_data, gi, bottom_blob_data, v_offset);
#endif
        }
    }
}
