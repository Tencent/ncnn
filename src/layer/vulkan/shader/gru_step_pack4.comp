// Copyright 2026 Futz12 <pchar.cn>
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout(binding = 0) readonly buffer bottom_blob { sfp bottom_blob_data[]; };
layout(binding = 1) readonly buffer weight_xc_blob { sfpvec4 weight_xc_data[]; };
layout(binding = 2) readonly buffer bias_c_blob { sfpvec4 bias_c_data[]; };
layout(binding = 3) readonly buffer weight_hc_blob { sfpvec4 weight_hc_data[]; };
layout(binding = 4) readonly buffer hidden_prev_blob { sfp hidden_prev_data[]; };
layout(binding = 5) writeonly buffer hidden_next_blob { sfp hidden_next_data[]; };
layout(binding = 6) writeonly buffer top_blob { sfp top_blob_data[]; };

layout(push_constant) uniform parameter
{
    int size;
    int num_output;
    int ti;
    int outw;
    int out_offset;
    int dir;
} p;

void main()
{
    int q_pack = int(gl_GlobalInvocationID.x);

    int num_output_pack = p.num_output / 4;
    if (q_pack >= num_output_pack)
        return;

    int x_offset = p.ti * p.size;

    int wxc_dir_base = p.dir * (3 * num_output_pack);
    int whc_dir_base = p.dir * (3 * num_output_pack);
    int bias_dir_base = p.dir * (4 * num_output_pack);

    int wxc_row_r = wxc_dir_base + 0 * num_output_pack + q_pack;
    int wxc_row_u = wxc_dir_base + 1 * num_output_pack + q_pack;
    int wxc_row_n = wxc_dir_base + 2 * num_output_pack + q_pack;

    int whc_row_r = whc_dir_base + 0 * num_output_pack + q_pack;
    int whc_row_u = whc_dir_base + 1 * num_output_pack + q_pack;
    int whc_row_n = whc_dir_base + 2 * num_output_pack + q_pack;

    afpvec4 r = buffer_ld4(bias_c_data, bias_dir_base + 0 * num_output_pack + q_pack);
    afpvec4 u = buffer_ld4(bias_c_data, bias_dir_base + 1 * num_output_pack + q_pack);

    for (int i = 0; i < p.size; i++)
    {
        afp xi = buffer_ld1(bottom_blob_data, x_offset + i);

        r += buffer_ld4(weight_xc_data, wxc_row_r * p.size + i) * xi;
        u += buffer_ld4(weight_xc_data, wxc_row_u * p.size + i) * xi;
    }

    for (int i = 0; i < p.num_output; i++)
    {
        afp hi = buffer_ld1(hidden_prev_data, i);

        r += buffer_ld4(weight_hc_data, whc_row_r * p.num_output + i) * hi;
        u += buffer_ld4(weight_hc_data, whc_row_u * p.num_output + i) * hi;
    }

    r = afpvec4(1.f) / (afpvec4(1.f) + exp(-r));
    u = afpvec4(1.f) / (afpvec4(1.f) + exp(-u));

    afpvec4 n = buffer_ld4(bias_c_data, bias_dir_base + 3 * num_output_pack + q_pack);

    for (int i = 0; i < p.num_output; i++)
    {
        afp hi = buffer_ld1(hidden_prev_data, i);
        n += buffer_ld4(weight_hc_data, whc_row_n * p.num_output + i) * hi;
    }

    afpvec4 wn = buffer_ld4(bias_c_data, bias_dir_base + 2 * num_output_pack + q_pack);
    n = wn + r * n;

    for (int i = 0; i < p.size; i++)
    {
        afp xi = buffer_ld1(bottom_blob_data, x_offset + i);
        n += buffer_ld4(weight_xc_data, wxc_row_n * p.size + i) * xi;
    }

    n = tanh(n);

    int q0 = q_pack * 4;

    afpvec4 hprev;
    hprev.r = buffer_ld1(hidden_prev_data, q0 + 0);
    hprev.g = buffer_ld1(hidden_prev_data, q0 + 1);
    hprev.b = buffer_ld1(hidden_prev_data, q0 + 2);
    hprev.a = buffer_ld1(hidden_prev_data, q0 + 3);

    afpvec4 h = (afpvec4(1.f) - u) * n + u * hprev;

    buffer_st1(hidden_next_data, q0 + 0, h.r);
    buffer_st1(hidden_next_data, q0 + 1, h.g);
    buffer_st1(hidden_next_data, q0 + 2, h.b);
    buffer_st1(hidden_next_data, q0 + 3, h.a);

    int out_index = p.ti * p.outw + p.out_offset + q0;
    buffer_st1(top_blob_data, out_index + 0, h.r);
    buffer_st1(top_blob_data, out_index + 1, h.g);
    buffer_st1(top_blob_data, out_index + 2, h.b);
    buffer_st1(top_blob_data, out_index + 3, h.a);
}
