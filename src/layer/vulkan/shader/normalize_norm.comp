// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_AMD_gpu_shader_half_float: require
#endif

layout (constant_id = 0) const int across_spatial = 0;
layout (constant_id = 1) const int across_channel = 0;
layout (constant_id = 2) const int channel_shared = 0;
layout (constant_id = 3) const int scale_term = 0;

layout (local_size_x_id = 233) in;
layout (local_size_y_id = 234) in;
layout (local_size_z_id = 235) in;

layout (binding = 0) buffer bottom_top_blob { sfp bottom_top_blob_data[]; };
layout (binding = 1) readonly buffer coeffs_blob { sfp coeffs_blob_data[]; };
layout (binding = 2) readonly buffer scale_blob { sfp scale_blob_data[]; };

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.w || gy >= p.h || gz >= p.c)
        return;

    const int gi = gz * p.cstep + gy * p.w + gx;

    afp v = sfp2afp(bottom_top_blob_data[gi]);

    afp a;

    if (across_spatial == 1 && across_channel == 1)
    {
        a = sfp2afp(coeffs_blob_data[0]);
    }

    if (across_spatial == 1 && across_channel == 0)
    {
        a = sfp2afp(coeffs_blob_data[gz]);
    }

    if (across_spatial == 0 && across_channel == 1)
    {
        a = sfp2afp(coeffs_blob_data[gy * p.w + gx]);
    }

    v = v * a;

    if (scale_term == 1)
    {
        if (channel_shared == 1)
        {
            v = v * sfp2afp(scale_blob_data[0]);
        }
        else
        {
            v = v * sfp2afp(scale_blob_data[gz]);
        }
    }

    bottom_top_blob_data[gi] = afp2sfp(v);
}
