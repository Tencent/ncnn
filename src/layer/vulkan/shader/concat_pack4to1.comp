// Tencent is pleased to support the open source community by making ncnn available.
//
// Copyright (C) 2019 THL A29 Limited, a Tencent company. All rights reserved.
//
// Licensed under the BSD 3-Clause License (the "License"); you may not use this file except
// in compliance with the License. You may obtain a copy of the License at
//
// https://opensource.org/licenses/BSD-3-Clause
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (constant_id = 0) const int axis = 0;

#define shape_constant_id_offset 1
layout (constant_id = shape_constant_id_offset + 0) const int dims = 0;
layout (constant_id = shape_constant_id_offset + 1) const int w = 0;
layout (constant_id = shape_constant_id_offset + 2) const int h = 0;
layout (constant_id = shape_constant_id_offset + 3) const int c = 0;
layout (constant_id = shape_constant_id_offset + 4) const int cstep = 0;

layout (constant_id = shape_constant_id_offset + 5) const int outdims = 0;
layout (constant_id = shape_constant_id_offset + 6) const int outw = 0;
layout (constant_id = shape_constant_id_offset + 7) const int outh = 0;
layout (constant_id = shape_constant_id_offset + 8) const int outc = 0;
layout (constant_id = shape_constant_id_offset + 9) const int outcstep = 0;

#if NCNN_image_shader
layout (binding = 0) uniform unfp sampler1D bottom_blob_1d;
layout (binding = 0) uniform unfp sampler2D bottom_blob_2d;
layout (binding = 0) uniform unfp sampler3D bottom_blob_3d;
layout (binding = 1, imfmtc1) writeonly uniform unfp image1D top_blob_1d;
layout (binding = 1, imfmtc1) writeonly uniform unfp image2D top_blob_2d;
layout (binding = 1, imfmtc1) writeonly uniform unfp image3D top_blob_3d;
#else
layout (binding = 0) readonly buffer bottom_blob { sfpvec4 bottom_blob_data[]; };
layout (binding = 1) writeonly buffer top_blob { sfp top_blob_data[]; };
#endif

layout (push_constant) uniform parameter
{
    int dims;
    int w;
    int h;
    int c;
    int cstep;

    int outdims;
    int outw;
    int outh;
    int outc;
    int outcstep;

    int offset;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= psc(w) || gy >= psc(h) || gz >= psc(c))
        return;

#if NCNN_image_shader
    if (psc(dims) == 1)
    {
        afpvec4 v = image1d_ld4(bottom_blob_1d, gx);

        int gx4 = gx * 4 + p.offset;

        image1d_st1(top_blob_1d, gx4 + 0, v.r);
        image1d_st1(top_blob_1d, gx4 + 1, v.g);
        image1d_st1(top_blob_1d, gx4 + 2, v.b);
        image1d_st1(top_blob_1d, gx4 + 3, v.a);
    }
    else if (psc(dims) == 2)
    {
        afpvec4 v = image2d_ld4(bottom_blob_2d, ivec2(gx, gy));

        if (axis == 0)
        {
            int gy4 = gy * 4 + p.offset;

            image2d_st1(top_blob_2d, ivec2(gx, gy4 + 0), v.r);
            image2d_st1(top_blob_2d, ivec2(gx, gy4 + 1), v.g);
            image2d_st1(top_blob_2d, ivec2(gx, gy4 + 2), v.b);
            image2d_st1(top_blob_2d, ivec2(gx, gy4 + 3), v.a);
        }
        if (axis == 1)
        {
            int gx4 = gx * 4 + p.offset;

            image2d_st1(top_blob_2d, ivec2(gx4 + 0, gy), v.r);
            image2d_st1(top_blob_2d, ivec2(gx4 + 1, gy), v.g);
            image2d_st1(top_blob_2d, ivec2(gx4 + 2, gy), v.b);
            image2d_st1(top_blob_2d, ivec2(gx4 + 3, gy), v.a);
        }
    }
    else // if (psc(dims) == 3)
    {
        afpvec4 v = image3d_ld4(bottom_blob_3d, ivec3(gx, gy, gz));

        if (axis == 0)
        {
            int gz4 = gz * 4 + p.offset;

            image3d_st1(top_blob_3d, ivec3(gx, gy, gz4 + 0), v.r);
            image3d_st1(top_blob_3d, ivec3(gx, gy, gz4 + 1), v.g);
            image3d_st1(top_blob_3d, ivec3(gx, gy, gz4 + 2), v.b);
            image3d_st1(top_blob_3d, ivec3(gx, gy, gz4 + 3), v.a);
        }
        if (axis == 1)
        {
            int gy4 = gy * 4 + p.offset;

            image3d_st1(top_blob_3d, ivec3(gx, gy4 + 0, gz), v.r);
            image3d_st1(top_blob_3d, ivec3(gx, gy4 + 1, gz), v.g);
            image3d_st1(top_blob_3d, ivec3(gx, gy4 + 2, gz), v.b);
            image3d_st1(top_blob_3d, ivec3(gx, gy4 + 3, gz), v.a);
        }
        if (axis == 2)
        {
            int gx4 = gx * 4 + p.offset;

            image3d_st1(top_blob_3d, ivec3(gx4 + 0, gy, gz), v.r);
            image3d_st1(top_blob_3d, ivec3(gx4 + 1, gy, gz), v.g);
            image3d_st1(top_blob_3d, ivec3(gx4 + 2, gy, gz), v.b);
            image3d_st1(top_blob_3d, ivec3(gx4 + 3, gy, gz), v.a);
        }
    }
#else
    const int gi = gz * psc(cstep) + gy * psc(w) + gx;

    ivec3 gxyz = ivec3(gx, gy, gz);

    gxyz[psc(dims) - 1] *= 4;
    gxyz[psc(dims) - 1 - axis] += p.offset;

    int v_offset_0 = gxyz.z * psc(outcstep) + gxyz.y * psc(outw) + gxyz.x;

    ivec3 gxyz4 = ivec3(1, psc(outw), psc(outcstep));

    ivec4 v_offset = v_offset_0 + ivec4(0, 1, 2, 3) * gxyz4[psc(dims) - 1 - axis];

    buffer_cp4to1(top_blob_data, v_offset, bottom_blob_data, gi);
#endif
}
