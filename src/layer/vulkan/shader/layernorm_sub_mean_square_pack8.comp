// Copyright 2025 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

layout (binding = 0) readonly buffer bottom_top_blob { sfpvec8 bottom_top_blob_data[]; };
layout (binding = 1) readonly buffer mean_blob { mat2x4 mean_data[]; };
layout (binding = 2) writeonly buffer square_blob { sfpvec8 square_blob_data[]; };

layout (push_constant) uniform parameter
{
    int w;
    int h;
    int c;
    int cstep;
    int affine_size;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.w || gy >= p.h || gz >= p.c)
        return;

    int group_id;
    if (p.affine_size == p.w)
    {
        group_id = gz * p.h + gy;
    }
    else // if (p.affine_size == p.w * p.h)
    {
        group_id = gz;
    }

    mat2x4 mean = mean_data[group_id];

    int linear_index = gz * p.cstep + gy * p.w + gx;

    mat2x4 v = mat2x4(buffer_ld8(bottom_top_blob_data, linear_index));

    v[0] = v[0] - mean[0];
    v[1] = v[1] - mean[1];

    v[0] = v[0] * v[0];
    v[1] = v[1] * v[1];

    buffer_st8(square_blob_data, linear_index, afpvec8(v));
}
