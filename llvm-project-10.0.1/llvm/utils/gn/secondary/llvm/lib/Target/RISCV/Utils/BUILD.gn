import("//llvm/utils/TableGen/tablegen.gni")

# Generates RISCVGenSystemOperands.inc
tablegen("RISCVGenSystemOperands") {
  visibility = [ ":Utils" ]
  args = [ "-gen-searchable-tables" ]
  td_file = "../RISCV.td"
}

static_library("Utils") {
  output_name = "LLVMRISCVUtils"
  public_deps = [
    ":RISCVGenSystemOperands",
  ]
  deps = [
    "//llvm/lib/MC",
    "//llvm/lib/Support",

    # MCTargetDesc depends on Utils, so we can't depend on the full
    # MCTargetDesc target here: it would form a cycle.
    "//llvm/lib/Target/RISCV/MCTargetDesc:tablegen",
  ]

  include_dirs = [ ".." ]
  sources = [
    "RISCVBaseInfo.cpp",
    "RISCVMatInt.cpp",
  ]
}
